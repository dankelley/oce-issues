c	This program version was modified in October 1992 to
c	incorporate a new calendar and a higher order approximation
c	for the astronomical arguments, their
c	derivatives, and the constituent frequencies.
c						M. Foreman
c
	program tide1
      character*5 KONTAB,KBLANK,KSTN,NA,IREF,KON,KONIN,DDUM,KO,KMPR
      character*5 NKON,NNKON,NKMPR
      character*1 icn
      DIMENSION NA(4),LSTRP(10)
      DIMENSION KONTAB(170),KON(100),C(100),S(100),FREQ(170),KMPR(170)
     1,SIG(100),ERC(100),ERS(100),NKON(15),A(100),EPS(100),XP(18000)
      COMMON Z(18000),IHH1,IDD1,IMM1,IYY1,ICC1,IHHL,IDDL,IMML,IYYL,
     1ICCL,KH1
	common /const/kontab,freq,mtot
      dimension AA(100),GD(100),KO(100)
      DATA KBLANK/'     '/
      DEGREE(CYC)=(CYC-IFIX(CYC)-SIGN(.5,CYC)+.5)*360.
	open(unit=4,file='tide2.dat',status='old',form='formatted')
	open(unit=8,file='tide3.dat',status='old',form='formatted')
	open(unit=2,file='file2.dat',status='unknown',form='formatted')
	open(unit=7,file='file7.dat',status='unknown',form='formatted')
	open(unit=9,file='file9.dat',status='unknown',form='formatted')
	open(unit=10,file='file10.dat',status='unknown',form='formatted')
c	open(unit=10,status='new',form='formatted')
c	open(unit=4,status='old',form='formatted')
c	open(unit=8,status='old',form='formatted')
c	open(unit=2,status='new',form='formatted')
c	open(unit=7,status='new',form='formatted')
c	open(unit=9,status='new',form='formatted')
C
C***********************************************************************
C
C     THIS VERSION OF THE FLEXIBLE ANALYSIS PROGRAM HAS BEEN
C     MODIFIED TO HANDLE DATA RECORDS WITH GAPS. THE LOW PASS FILTERING
C     HAS BEEN REMOVED. SUBROUTINE INPUT IS USED TO READ IN THE DATA
C     SUBROUTINES UCON AND SCFIT2 FROM THE TIDAL STREAMS PROGRAM
C     WRITTEN BY J. TAYLOR WERE ADAPTED FOR USE BY THIS PROGRAM.
C
C                                           J. MINAKER  DEC. 1973.
C*********************************************************************
C*
C*    THE PRESENT PROGRAM VERSION WAS REVISED IN JULY 1976 BY
C*    M FOREMAN. SEE THE USER MANUAL FOR DETAILS.
C*
C***********************************************************************
C*  FILE REFERENCE NUMBERS OF DEVICES REQUIRED BY THIS PROGRAM
C*      KR  - INPUT FILE  - CONTAINS THE TIDAL CONSTITUENT INFORMATION.
C*      MTD - INPUT FILE  - GIVES TIDAL STATION AND ANALYSIS TYPE
C*                          DETAILS ALONG WITH THE HOURLY HEIGHTS.
C*      LP  - OUTPUT FILE - LINE PRINTER.
C*      KP  - OUTPUT FILE - CONTAINS THE ANALYSIS RESULTS, IF SO
C*                          REQUESTED.
C*      7   - OUTPUT FILE - CONTAINS RESULTS FOR ALPHATEXT OR
C*                          FLEXOWRITER, IF SO REQUESTED.
C*      9   - OUTPUT FILE - TEMPORARY DISC FILE FOR STORING PRELIMINARY
C*                          OUTPUT AND TERMINATING THE PROGRAM.
C*      KPR - OUTPUT FILE - IF SO REQUESTED, CONTAINS HOURLY SYNTHESIZED
C*                          VALUES BASED ON ANALYSIS RESULTS.
C*  PRESENTLY KR,MTD,LP,KP, AND KPR ARE ASSIGNED THE RESPECTIVE  NUMBERS
C*  8,4,6,2, AND 10.  SEE THE MANUAL, OR COMMENT STATEMENTS WITHIN
C*  WITHIN THIS PROGRAM FOR FURTHER DETAILS ON THEIR USE.
C*
C***********************************************************************
C*  IF THE SAMPLING INTERVAL OF THE ORIGINAL DATA IS OTHER THAN
C*  ONE HOUR, THEN THE RECORD SHOULD BE FILTERED AND DECIMATED TO HOURLY
C*  INTERVALS, PRIOR TO SUBMISSION TO THIS PROGRAM. G GODIN SUGGESTS THE
C*  SEQUENCE OF MOVING AVERAGE FILTERS A12*A12*A13 FOR 5 MINUTE DATA,
C*  AND A6*A6*A7 FOR 10 MINUTE DATA.
C*
C***********************************************************************
C*  ARRAY DEFINITIONS AND DIMENSION GUIDELINES.
C*
C*  MTOT           IS THE NUMBER OF CONSTITUENTS INCLUDED IN THE DATA
C*                 FILE. AT PRESENT THIS IS 146.
C*  M              IS THE NUMBER OF CONSTITUENTS INCLUDED IN THE
C*                 ANALYSIS. AT PRESENT IT WILL HAVE A MAXIMUM OF 69 +
C                  THE NUMBER OF SHALLOW WATER CONSTITUENTS SPECIALLY
C*                 DESIGNATED FOR INCLUSION (NOT IN THE STANDARD 69
C*                 CONSTITUENT PACKAGE) IN THE ANALYSIS.
C*  KONTAB(I)      IS THE ARRAY CONTAINING ALL THE CONSTITUENT NAMES AS
C*                 THEY ARE READ IN FROM THE DATA FILE. IT SHOULD HAVE
C*                 THE MINIMUM DIMENSION MTOT.
C*  KON(I)         IS THE ARRAY CONTAINING THE NAMES OF ALL THE
C*                 CONSTITUENTS THAT ARE TO BE INCLUDED IN THE LEAST
C*                 SQUARES ANALYSIS TO THE TIDAL RECORD. THEY ARE CHOSEN
C*                 FROM THE KONTAB LIST VIA THE RAYLEIGH CRITERION. THE
C*                 ARRAY SHOULD HAVE DIMENSION AT LEAST M.
C*  FREQ(I)        IS THE ARRAY OF FREQUENCIES (CYCLES/ HR.) CORRESPOND-
C*                 ING TO THE CONSTITUENTS CONTAINED IN KONTAB. IT
C*                 SHOULD HAVE DIMENSION AT LEAST MTOT.
C*  KMPR(I)        IS THE ARRAY OF CONSTITUENTS TO WHICH THOSE IN KONTAB
C*                 SHOULD BE COMPARED UNDER THE RAYLEIGH CRITERION. IT
C*                 SHOULD HAVE DIMENSION AT LEAST MTOT.
C*  NKON(I)        IS THE ARRAY CONTAINING THE NAMES OF ALL ADDITIONAL
C*                 CONSTITUENTS, ALONG WITH THE STANDARD 69, TO BE
C*                 CONSIDERED FOR INCLUSION IN THE ANALYSIS.
C*  C(I) &amp; S(I)    ARE THE ARRAYS CONTAINING THE COS AND SIN COEFFICIENTS
C*                 OF THE I-TH CONSTITUENT AS FOUND VIA THE LEAST
C*                 SQUARES ANALYSIS. THEIR MINIMUM DIMENSION SHOULD BE
C*                 M.
C*  SIG(I)         IS THE ARRAY OF FREQUENCIES CORRESPONDING TO THE
C*                 CONSTITUENTS CONTAINED IN KON. IT SHOULD HAVE MINIMUM
C*                 DIMENSION M.
C*  ERC(I) &amp; ERS(I) ARE THE ARRAYS CONTAINING ESTIMATES OF THE ERRORS
C*                 (STANDARD DEVIATIONS) FOR THE VALUES CONTAINED IN THE
C*                 ARRAYS C(I) &amp; S(I) RESPECTIVELY. THEIR MINIMUM
C*                 DIMENSION SHOULD BE M.
C*  Z(I)           IS THE ARRAY CONTAINING ALL THE HOURLY HEIGHTS OF THE
C*                 DATA RECORD. IT HAS BEEN GIVEN THE DIMENSION SIZE OF
C*                 9000 WHICH IS SUFFICIENT FOR RECORDS OF LENGTH UP TO
C*                 ONE YEAR.
C*  XP(I)          IS THE ARRAY CONTAINING THE SYNTHESIZED HOURLY
C*                 HEIGHTS BASED ON THE ANALYSIS RESULTS.  PREDICTIONS
C*                 ARE SPECIFIED FOR THE ANALYSIS PERIOD, INCLUDING
C*                 THOSE INTERVALS WHERE THERE WERE GAPS IN THE ORIGINAL
C*                 RECORD.  AS WITH Z, THIS ARRAY HAS BEEN GIVEN THE
C*                 DIMENSION SIZE OF 9000.
C*  AS(I,J)        IS THE TWO DIMENSIONAL ARRAY CONTAINING THE UPPER
C*                 TRIANGLE AND DIAGONAL OF THE  ARRAY
C*                 RESULTING FROM THE LEAST SQUARES FIT OF CONSTITUENT
C*                 AMPLITUDE AND PHASE TO THE HOURLY RECORD. IT SHOULD
C*                 HAVE A MINIMUM DIMENSION OF 2*M-1 BY 2*M-1.
C*  A(I) &amp; EPS(I)  ARE THE ARRAYS CONTAINING THE AMPLITUDE AND PHASE FOR
C*                 CONSTITUENT KON(I) AS FOUND VIA THE LEAST SQUARES
C*                 ANALYSIS. THEY SHOULD HAVE A MINIMUM DIMENSION OF
C*                 M.
C*  KO(I)          IS THE ARRAY CONTAINING THE NAMES OF ALL THE CONSTI-
C*                 TUENTS IN KON AND INFERRED FROM THOSE IN KON. ITS
C*                 MINIMUM DIMENSION SHOULD BE M.
C*  AA(I) &amp; GD(I)  ARE THE ARRAYS CONTAINING THE AMPLITUDE AND PHASE FOR
C*                 CONSTITUENT KO(I) AFTER CORRECTIONS FOR NODAL MODULA-
C*                 TION, ASTRONOMICAL ARGUMENT AND INFERRED CONSTITUENT.
C*                 THEIR MINIMUM DIMENSION SHOULD BE M.
C***********************************************************************
C
C
C JOB INITIALIZATION
C
      KR=8
      KP=2
      LP=6
      MTD=4
      KPR=10
      RAY=1.0
C
C***********************************************************************
C*  READ FROM DEVICE MTD, THE PARAMETERS IOUT1,RAYOPT,ZOFF,ICHK,OBSFAC,
C*  INDPR,AND NSTRP, IN THE FORMAT (I2,2X,F4.2,2X,F10.0,I2,3X,F10.7,2I5)
C*     IOUT1  = 6 IF ONLY LINE PRINTER LISTING OF RESULTS IS DESIRED.
C*              2 IF CARD OUTPUT AND LISTING ARE BOTH DESIRED.
C*     RAYOPT = RAYLEIGH CRITERION VALUE IF DIFFERENT FROM 1.0.
C*     ZOFF   = CONSTANT VALUE TO BE SUBTRACTED FROM ALL THE HOURLY
C*              HEIGHTS.
C*     ICHK   = 0 IF THE HOURLY HEIGHTS INPUT DATA IS TO BE CHECKED FOR
C*                ERRORS
C*              1 IF NO CHECKING IS DESIRED.
C*     OBSFAC = THE FACTOR, IF DIFFERENT FROM .01, THAT WILL MULTIPLY
C*              THE INTEGER HOURLY OBSERVATIONS IN ORDER TO PRODUCE THE
C*              DESIRED UNITS FOR THE FINAL CONSTITUENT AMPLITUDES.
C*     INDPR  = 1 IF HOURLY HEIGHT PREDICTIONS BASED ON THE
C*                ANALYSIS RESULTS, ARE TO BE CALCULATED AND WRITTEN
C*                ONTO DEVICE KPR.
C*                IF THERE IS INFERENCE, THIS PARAMETER VALUE WILL
C*                ALSO GIVE THE ROOT MEAN SQUARE RESIDUAL ERROR AFTER
C*                INFERENCE ADJUSTMENTS HAVE BEEN MADE.
C*            = 0 IF NO SUCH PREDICTIONS ARE DESIRED.
C*     NSTRP  = THE NUMBER OF SUCCESSIVE MOVING AVERAGE FILTERS THAT
C*              HAVE BEEN APPLIED TO THE ORIGINAL DATA. IF
C*              NSTRP IS NONZERO, THEN SUITABLE AMPLITUDE CORRECTIONS
C*              WILL BE MADE TO COMPENSATE FOR THE SMOOTHING EFFECT
C*              OF THE FILTERS.
C*
C*  IF NSTRP IS GREATER THAN ZERO, THEN READ FROM DEVICE MTD, THE
C*  PARAMETER, TIMINT AND THE ARRAY (LSTRP(I),I=1,NSTRP), IN THE FORMAT
C*  (F10.5,10I5).
C*      TIMINT = THE SAMPLING INTERVAL, IN MINUTES, OF THE ORIGINAL
C*               UNFILTERED RECORD.
C*      LSTRP  = THE ARRAY CONTAINING THE NUMBER OF TERMS USED IN EACH
C*               OF THE NSTRP SUCCESSIVE MOVING AVERAGE FILTERS.
C
      READ(MTD,1) IOUT1,RAYOPT,ZOFF,ICHK,OBSFAC,INDPR,NSTRP
1     FORMAT(I2,2X,F4.2,2X,F10.0,I2,3X,F10.7,2I5)
      IF(NSTRP.LE.0) GO TO 11
      READ(MTD,12) TIMINT,(LSTRP(I),I=1,NSTRP)
      DELT=TIMINT/60.
   12 FORMAT(F10.5,10I5)
   11 CONTINUE
      IF(RAYOPT.GE.0.32) RAY=RAYOPT
      IF(OBSFAC.LT.1.E-8) OBSFAC=0.01
      PI=3.1415926536
      TWOPI=2.*PI
C
C***********************************************************************
C* CONSTITUENTS, FREQUENCIES, &amp; RAYLEIGH COMPARISON PAIRS ARE READ ON
C* DEVICE KR.
C
      DO 1020 K=1,1000
      READ(KR,1010) KONTAB(K),FREQ(K),KMPR(K)
 1010 FORMAT(4X,A5,3X,F13.10,4X,A5)
      IF(KONTAB(K).eq.KBLANK) goto 1040
1020  CONTINUE
1040  MTOT=K-1
C
C***********************************************************************
C* ASTRONOMICAL ARGUMENTS, SATELLITES TO THE MAIN CONSTITUENTS, AND
C* SHALLOW WATER CONSTITUENTS ARE READ ON DEVICE KR.
C
      CALL OPNVUF (IDUM,DDUM,DUM,DUM,DUM)
C
C***********************************************************************
C* INFERENCE PAIRS OF CONSTITUENTS ARE READ ON DEVICE MTD.
C
      CALL OPNINF(DDUM,IDUM,IDUM,DDUM,DUM,DUM,DDUM,DUM,DUM,DUM,NIN)
C
C***********************************************************************
C*  CONSTITUENTS OTHER THAN THOSE IN THE STANDARD 69 MEMBER PACKAGE
C*  CAN BE SPECIFIED NOW ON DEVICE MTD ALONG WITN THEIR RAYLEIGH COMPAR-
C*  ISON CONSTITUENT (WHICH SHOULD BE IN THE STANDARD OR NOW AUGMENTED
C*  DATA PACKAGE). IF THE RAYLEIGH COMPARISON CONSTITUENT IS MISSING
C*  THE ADDITIONAL CONSTITUENT WILL NOT BE INCLUDED IN THE LEAST SQUARES
C*  FIT.  THE FORMAT IS  (6X,A5,4X,A5) AND A BLANK CARD DENOTES THE END
C*  OF THESE ADDITIONAL CONSTITUENTS.
C
      NADD=0
  994 READ(MTD,999) NNKON,NKMPR
      IF(NNKON.EQ.KBLANK) GO TO 995
  999 FORMAT(6X,A5,4X,A5)
      DO 998 K=1,MTOT
      IF(KONTAB(K).EQ.NNKON) GO TO 997
  998 CONTINUE
      WRITE(LP,996) NNKON
  996 FORMAT(5X,'CONSTITUENT',2X,A5,2X,'IS NOT INCLUDED IN THE LIST OF
     1 POSSIBLE ADDITIONAL CONSTITUENTS')
      STOP
  997 KMPR(K)=NKMPR
      NADD=NADD+1
      NKON(NADD)=NNKON
      GO TO 994
  995 CONTINUE
C
C***********************************************************************
C*  CASE INITIALIZATION
C*
C*  READ ON DEVICE MTD THE FOLLOWING INFORMATION ON THE PERIOD OF THE
C*  DESIRED ANALYSIS : INDY,IHH1,IDD1,IMM1,IYY1,ICC1,IHHL,IDDL,IMML,
C*  IYYL,ICCL IN THE FORMAT (I1,1X,10I2)
C*     INDY   = 8 INDICATES AN ANALYSIS IS DESIRED FOR THE UPCOMING
C*                 PERIOD.
C*              0 INDICATES NO FURTHER ANALYSES ARE DESIRED.
C*     IHH1,IDD1,IMM1,IYY1,ICC1 = HOUR, DAY, MONTH, YEAR, CENTURY OF 
C*                                THE BEGINNING OF THE ANALYSIS.
C*     IHHL,IDDL,IMML,IYYL,ICCL = HOUR, DAY, MONTH, YEAR, CENTURY OF 
C*                                THE END OF THE ANALYSIS PERIOD.
C*
C*  FOLLOWED BY INFORMATION ON THE TIDAL STATION,
C*
C*     INDIC,KSTN,(NA(J),J=1,4),ITZON,LAD,LAM,LOD,LOM,IREF,
C*     IN THE FORMAT (I1,4X,A5,3A6,A4,A3,1X,2I2,I3,I2,5X,A5).
C*     INDIC  = 1 IF J CARD OUTPUT IS DESIRED
C*              OTHERWISE IF NOT.
C*     KSTN   = STATION NUMBER
C*     NA     = STATION NAME
C*     ITZONE = TIME ZONE OF HOURLY HEIGHTS.
C*     LAD,LAM= LATITUDE IN DEGREES &amp; MINUTES OF THE TIDAL STATION
C*     LOD,LOM= LONGITUDE IN DEGREES &amp; MINUTES OF THE TIDAL STATION
C*     IREF   = REFERENCE STATION NUMBER.
C*
C
      READ(MTD,1060) INDY,IHH1,IDD1,IMM1,IYY1,ICC1,IHHL,IDDL,IMML,
     1IYYL,ICCL
1060  FORMAT(I1,1X,10I2)
1050  READ(MTD,1111)INDIC,KSTN,(NA(J),J=1,4),ITZONE,LAD,LAM,LOD,LOM,IREF
1111  FORMAT(I1,4X,A5,4a5,2x,A3,I3,I2,I3,I2,5X,A5)
C
C***********************************************************************
C*  UNLESS SPECIFIED OTHERWISE, A STATION LATITUDE OF 50 DEGREES IS
C*  ASSUMED FOR THE PURPOSE OF CALCULATING SATELLITE TO MAIN
C*  CONSTITUENT AMPLITUDE RATIOS.
C
C
C	DEFAULT CENTURY
	IF(ICC1.EQ.0) ICC1=19
	IF(ICCL.EQ.0) ICCL=19
C
      XLAT=LAD+ISIGN(LAM,LAD)/60.
      IF(ABS(XLAT).LT.5.) XLAT=SIGN(5.,XLAT)
      IF(IDD1)1080,1070,1080
1070  STOP
C
C***********************************************************************
C*   HERE THE NUMBER OF USEABLE OBSERVATIONS (NOBSU) &amp; THE TIME OF THE
C*   MIDDLE OBSERVATION ARE FOUND.
C
1080  CALL GDAY(IDD1,IMM1,IYY1,ICC1,KD)
      KH1=KD*24+IHH1
      CALL GDAY(IDDL,IMML,IYYL,ICCL,KD)
      KHL=KD*24+IHHL
      NOBS=KHL-KH1+1
      NOBSU=((NOBS-1)/2)*2+1
      KHM=KH1+NOBSU/2
      KD=KHM/24
      IHHM=KHM-24*KD
      CALL DMY(IDDM,IMMM,IYYM,ICCM,KD)
C
C***********************************************************************
C*  HERE V (= ASTRONOMICAL PHASE ARGUMENT), U &amp; F (NODAL MODULATION
C*  PHASE AND AMPLITUDE CORRECTIONS) ARE CALCULATED FOR ALL MTOT
C*  CONSTITUENTS.
C
      CALL SETVUF(KHM,DDUM,DUM,DUM,XLAT)
C
C
C***********************************************************************
C   DETERMINE USABLE CONSTITUENTS ACCORDING TO THE RAYLEIGH CRITERION.
C   M IS THE NUMBER FOUND
C
      CALL UCON(KONTAB,FREQ,KMPR,MTOT,NOBSU,KON,SIG,M,RAY)
C
C***********************************************************************
C   READ IN DATA TO BE ANALYZED
C
      CALL INPUT(MTD,ZOFF,ISTN,N,ICN,ICHK,OBSFAC)
       IF(N.EQ.0) GO TO 4000
C
C***********************************************************************
C   FIND THE LEAST SQUARES FIT
C
      IND=INDPR
      IF(IND.EQ.1.AND.NIN.GT.0) IND=0
      CALL SCFIT2(Z,N,SIG,M,IND,C,S,ERC,ERS,G,NX,AVX,SDX,RT,RA,XP)
C
C***********************************************************************
C*  OUTPUT PRELIMINARY RESULTS
C*  FOR OUTPUT PURPOSES, THE NUMBER OF ACCURATE DIGITS IN THE CONSTI-
C*  TUENT FREQUENCIES IS MACHINE DEPENDENT. SINGLE PRECISION REAL
C*  VARIABLES HAVE APPROXIMATELY 7 ACCURATE SIGNIFICANT DIGITS ON AN IBM
C*  MACHINE AND APPROXIMATELY 11 ON A CDC.  THE FORMAT STATEMENTS
C*  SHOULD BE ADJUSTED SO AS NOT TO SHOW 'GARBAGE' FIGURES IN THE LAST
C*  FEW DECIMAL PLACES.
C
      WRITE(LP,2) ISTN,(na(i),i=1,4),itzone
2     FORMAT('1STATION',I6,'  PRELIMINARY RESULTS',5x,4a5,5x,a3/
     1 '0CONSTITUENT  FREQUENCY',9X,'C',7X,'ERR',8X,'S',7X,'ERR'/)
      DO 3 K=1,M
      WRITE(LP,4) K,KON(K),SIG(K),C(K),ERC(K),S(K),ERS(K)
    4 FORMAT(1X,I3,2X,A5,F13.8,2(5X,2F7.3))
3     CONTINUE
      WRITE(LP,5) NX,AVX,SDX,RT,G
5     FORMAT('0NUMBER OF VALID DATA =',I6,'  AVERAGE =',F6.2,
     1 '  STANDARD DEVIATION =',F6.2,'  THEORETICAL RMS =',F6.2,
     2 '  MATRIX CONDITION =',F6.2)
  490 FORMAT('0','THE PREVIOUS C AND S VALUES WILL BE SCALED TO COMPENSA
     1TE FOR THE PRIOR APPLICATION OF MOVING AVERAGE FILTERS',/,30X,
     1'ORIGINAL DT=',F7.5,' HR',4X,'FILTERS =',5I3)
      IF(NSTRP.GT.0) WRITE(LP,490) DELT,(LSTRP(I),I=1,NSTRP)
C
C***********************************************************************
C*  SCALE UP THE LEAST SQUARES FIT RESULTS TO COMPENSATE FOR PRE-
C*  FILTERING.
C
      CALL SCLUP(LSTRP,NSTRP,DELT,SIG,C,S,M,1)
C
C***********************************************************************
C*  CHANGE RESULTS TO POLAR FORM IN ARRAYS A AND EPS.
C*  OUTPUT RESULTS ON BOTH PRINTER AND PUNCH. OUTPUT FIRST WITHOUT
C*  INFERENCE AND THEN WITH INFERENCE IF THERE IS ANY.
C
      DO 5005 K=1,M
      AK=SQRT(C(K)*C(K)+S(K)*S(K))
      EPS(K)=ATAN2(S(K),C(K))/TWOPI
5005  A(K)=AK
      MIN=M
      NMAX=2
      IF(NIN.EQ.0) NMAX=1
      DO 5350 NOYES=1,NMAX
C
C***********************************************************************
C*  IF THERE ARE INFERRED CONSTITUENTS, THE RESULTS ARE OUTPUT TWICE;
C*  THE FIRST TIME WITHOUT INFERENCE AND THE SECOND TIME WITH.
C*  WRITE OUT TITLES
C
	write(6,66) (na(i),i=1,4),itzone
66	format('1',4a5,5x,a3)
      DO 5352 LK=IOUT1,LP,4
 5352 WRITE(LK  ,5110)ISTN,IHH1,IDD1,IMM1,IYY1,IHHL,IDDL,IMML,IYYL,
     2  NOBS,N    ,IHHM,IDDM,IMMM,IYYM,RAY
5110  FORMAT(38H ANALYSIS OF HOURLY TIDAL HEIGHTS  STN,I6,I6 ,
     2  1HH ,I3,1H/,I2,1H/,I2,4H TO ,I3,1HH ,I3,1H/,I2,1H/,I2   /
     3 9H NO.OBS.= ,I6,17H    NO.PTS.ANAL.= ,I6,10H    MIDPT= ,I2,
     4  1HH ,I3,1H/,I2,1H/,I2 , '  SEPARATION =',
     5 F4.2//'  NO NAME    FREQUENCY   STN  M-Y/ M-Y',
     6 7X,'A',6X,'G',18X,'AL',5X,'GL')
C
C***********************************************************************
C*  OUTPUT FOR EACH CONSTITUENT IN TURN
C*  NUMOUT IS THE NUMBER OF CONSTITUENTS LISTED IN THE OUTPUT (THOSE
C*  CHOSEN VIA RAYLEIGH CRITERION AND THOSE INFERRED).
C
      NUMOUT=0
      DO 5300 K=1,M
      AAN=A(K)
      EPSAN=EPS(K)
      IF(NOYES.EQ.1) GO TO 5220
C
C***********************************************************************
C*  EVERY CONSTITUENT INCLUDED IN THE LEAST SQUARES ANALYSIS IS CHECKED
C*  AGAINST THOSE USED FOR INFERENCE AND THOSE TO BE INFERRED. IF A
C*  CONSTITUENT WAS TO BE INFERRED BUT WAS INCLUDED IN THE LEAST SQUARES
C*  FIT, THEN INFERENCE IS IGNORED.
C
      CALL INFER(KON,M,NOBSU,KON(K),AAN,EPSAN,KONIN,AIN,EPSIN,SIGIN,NIN
     1)
      IF(KONIN.eq.KBLANK) go to 5220
 5150 MIN=MIN+1
      IF(INDPR.EQ.0) GO TO 5160
      ARG=TWOPI*EPSIN
      C(MIN)=AIN*COS(ARG)
      S(MIN)=AIN*SIN(ARG)
      SIG(MIN)=SIGIN
      ARG=TWOPI*EPSAN
      C(K)=AAN*COS(ARG)
      S(K)=AAN*SIN(ARG)
C
C***********************************************************************
C*  OUTPUT WITH INFERENCE
C
 5160 CALL VUF(KH,KON(K),VUAN,FAN,XLAT)
      CALL VUF(KH,KONIN,VUIN,FIN,XLAT)
      EPSAND=DEGREE (EPSAN)
      EPSIND=DEGREE (EPSIN)
      GAND=DEGREE (EPSAN+VUAN)
      GIND=DEGREE (EPSIN+VUIN)
      AAAN=AAN/FAN
      AAIN=AIN/FIN
      NUMORG =NUMOUT
C
C***********************************************************************
C*  HERE IT IS DETERMINED WHICH OF THE INFERRED OR ANALYZED CONSTITUENT
C*  IS LISTED FIRST IN THE OUTPUT (ACCORDING TO INCREASING FREQUENCY).
C
      IF(SIG(K)-SIGIN)5170,5170,5190
5170  NUMOUT=NUMOUT+1
      DO 5353 LK=IOUT1,LP,4
 5353 WRITE(LK,5230)NUMOUT,KON(K),SIG(K),ISTN,IMM1,IYY1,IMML,IYYL,
     1  AAAN,GAND,AAN,EPSAND
      KO(NUMOUT)=KON(K)
      AA(NUMOUT)=AAAN
      GD(NUMOUT)=GAND
      IF(NUMOUT-NUMORG-1)5300,5190,5300
5190  NUMOUT=NUMOUT+1
      DO 5354 LK=IOUT1,LP,4
 5354 WRITE(LK,5200)NUMOUT,KONIN,SIGIN,ISTN,IMM1,IYY1,IMML,IYYL,
     1  AAIN,GIND,KON(K),AIN, EPSIND
 5200 FORMAT(1X,I3,1X,A5,F12.8,I6,1X,2I2,1H/,2I2,F8.4,F7.2,
     1 ' INF FR ',A5,F7.4,F7.2)
      KO(NUMOUT)=KONIN
      AA(NUMOUT)=AAIN
      GD(NUMOUT)=GIND
      IF(NUMOUT-NUMORG-1)5300,5170,5300
C
C***********************************************************************
C*  OUTPUT WITHOUT INFERENCE
C
 5220 CALL VUF(KH,KON(K),VU,F,XLAT)
      NUMOUT=NUMOUT+1
      AA(NUMOUT)=A(K)/F
      GD(NUMOUT)=DEGREE(EPS(K)+VU)
      EPSD=DEGREE (EPS(K))
      KO(NUMOUT)=KON(K)
      AW=AA(NUMOUT)
      GW=GD(NUMOUT)
      DO 5355 LK=IOUT1,LP,4
 5355 WRITE(LK,5230) NUMOUT,KON(K),SIG(K),ISTN,IMM1,IYY1,IMML,IYYL,
     1  AW,GW,A(K),EPSD
 5230 FORMAT(1X,I3,1X,A5,F12.8,I6,1X,2I2,1H/,2I2,F8.4,F7.2,
     1  12X,F8.4,F7.2)
5300  CONTINUE
C
C******************************************R****************************
C*  IF SO DESIRED, HOURLY HEIGHTS AS PREDICTED FROM THE ANALYSIS RESULTS
C*  ARE CALCULATED HERE.
C
      IF(NOYES.EQ.1.OR.INDPR.EQ.0) GO TO 5351
      CALL SCLUP(LSTRP,NSTRP,DELT,SIG,C,S,MIN,-1)
      CALL SCFIT2(Z,N,SIG,MIN,2,C,S,ERC,ERS,G,NX,AVX,SDX,RT,RA,XP)
      WRITE(LP,495) RA
  495 FORMAT(/' AFTER INFERENCE, RMS(RESID ERROR)=',F8.5)
 5351 CONTINUE
C
C***********************************************************************
5350  CONTINUE
C
C***********************************************************************
C*  IF SO REQUESTED, OUTPUT PREDICTED RESULTS ON DEVICE KPR IN THE SAME
C*  UNITS, SCALE AND FORMAT AS THE ORIGINAL INPUT.
C
      IF(INDPR.EQ.0) GO TO 510
      CALL OUTPUT(XP,N,KPR,OBSFAC,ISTN)
  510 CONTINUE
C
C***********************************************************************
C*  READ UNTIL NEXT PERIOD CARD
C
4000  READ(MTD,1060) INDY,IHH1,IDD1,IMM1,IYY1,IHHL,IDDL,IMML,IYYL
      IF(INDY.EQ.0) GO TO 2012
      IF(INDY.NE.8) GO TO 4000
      GO TO 1050
 2012 CONTINUE
      STOP
      END
      SUBROUTINE SCLUP(LSTRP,NSTRP,DELT,SIG,CX,SX,M,IND)
C
C***********************************************************************
C*  SCALES UP RESULTS OF TIDAL HEIGHTS ANALYSIS WHEN DATA HAS BEEN
C*  PRE-FILTERED  AND RESPONSE OF THE HIGHER FREQUENCIES IS SOMEWHAT
C*  LESS THAN 100 PERCENT .   THE FILTER IS TAKEN TO BE ...
C*  A(LSTRP1)*A(LSTRP2)* ... *A(LSTRP(NSTRP)) / LSTRP1* ... LSTRP(NSTRP) .
C*  DELT IS THE SPACING IN HOURS OF THE RAW DATA .
C*  SIG(1...M) IS THE ARRAY OF CONSTITUENT FREQS IN CYCLES PER HOUR .
C*  CX,SX(1...M) ARE THE ARRAYS OF CONSTITS TO BE SCALED UP .
C*  FOR NORMAL USE SET IND=1. IF IND=-1 THE SCALING IS REVERSED.
C*  APRIL 1973   SEE ANALYSIS OF TIDES BY G GODIN   PAGE 62.
C
      DIMENSION LSTRP(10),CX(100),SX(100),SIG(100)
      IF(NSTRP.EQ.0) RETURN
      PI=3.1415926535
      DO 130 K=1,M
      IF(SIG(K).LE. 1.E-8) GO TO 130
      ANG=PI*DELT*SIG(K)
      DEN=SIN(ANG)
      DO 100 I=1,NSTRP
      FACTR=SIN(LSTRP(I)*ANG)/(LSTRP(I)*DEN)
      IF(IND.EQ.-1) FACTR=1.0/FACTR
      CX(K)=CX(K)/FACTR
      SX(K)=SX(K)/FACTR
100   CONTINUE
130   CONTINUE
      RETURN
      END
      SUBROUTINE OUTPUT(XP,N,KPR,OBSFAC,KSTN)
      DIMENSION XP(18000),MONTH(12),IHT(24)
      COMMON Z(18000),IHS,IDS,IMS,IYS,ICS,IHE,IDE,IME,IYE,
     1ICE,KH1
      DATA MONTH/31,28,31,30,31,30,31,31,30,31,30,31/
C
C***********************************************************************
C*  THIS SUBROUTINE WRITES THE PREDICTED HOURLY HEIGHTS, XP, ONTO FILE
C*  REFERENCE NUMBER KPR. IN ORDER THAT THE VALUES WILL BE COMPARIBLE
C*  TO THOSE ORIGINALLY INPUT FOR ANALYSIS, THEY ARE DIVIDED BY OBSFAC
C*  AND ROUNDED TO INTEGERS BEFORE BEING OUTPUT IN THE SAME FORMAT.
C*  PREDICTIONS ARE SPECIFIED FOR THE DESIRED ANALYSIS PERIOD ONLY,
C*  INCLUDING INTERVALS WHERE THERE WERE GAPS IN THE ORIGINAL RECORD.
C*  VALUES OUTSIDE THIS PERIOD ARE SHOWN AS 9999.
C*
C***********************************************************************
C*  IMPORTANT: CHECK THAT FOR YOUR COMPUTER INSTALLATION, INT HAS THE
C*             FOLLOWING MEANING.
C*                 INT(X) = SIGN(X)*N  WHERE N IS THE LARGEST INTEGER
C*                          LESS THAN OR EQUAL TO ABS(X).
C*             IF IT DOES NOT, THEN A STATEMENT FUNCTION THAT PERFORMS
C*             THIS OPERATION WILL HAVE TO BE DEFINED.
C***********************************************************************
C*
C*  INITIALIZATIONS
C
      K99=9999
      MODC=ICS-4*(ICS/4)
      IF(IYS.EQ.0.AND.MODC.EQ.0) MONTH(2)=29
      MODY=IYS-4*(IYS/4)
      IF(IYS.NE.0.AND.MODY.EQ.0) MONTH(2)=29
      DO 18 J=1,N
      XX=XP(J)/OBSFAC
   18 XP(J)=XX+SIGN(.5,XX)
      ID=IDS
      IM=IMS
      IY=IYS
	IC=ICS
C
C***********************************************************************
C*  OUTPUT THE FIRST DAYS RESULTS
C
      DO 19 J=1,24
   19 IHT(J)=K99
      K=0
      DO 20 J=IHS,24
      K=K+1
   20 IHT(J)=INT(XP(K))
   21 FORMAT('1',2X,I4,4X,I2,1X,3I2,12I4/'2',2X,I4,4X,I2,1X,3I2,12I4)
      WRITE(KPR,21) KSTN,IC,ID,IM,IY,(IHT(L),L=1,12),KSTN,IC,ID,IM,IY
     1,(IHT(L),L=13,24)
C
C***********************************************************************
C*  OUTPUT THE REMAINING DAYS HERE
C
      NRHR=N-(24-IHS+1)
      NDAY=NRHR/24
      IF(24*NDAY.NE.NRHR) NDAY=NDAY+1
      DO 27 J=1,NDAY
      DO 28 I=1,24
      K=K+1
      IF(K.GT.N) GO TO 30
   28 IHT(I)=INT(XP(K))
      GO TO 33
   30 DO 32 L=I,24
   32 IHT(L)=K99
   33 CONTINUE
      ID=ID+1
      IF(ID.LE.MONTH(IM)) GO TO 29
      ID=1
      IM=IM+1
      IF(IM.LE.12) GO TO 29
      IM=1
      IY=IY+1
	IF(IY.EQ.100) THEN
	IY=0
	IC=IC+1
	MOD=IC-(IC/4)*4
	IF(MOD.EQ.0) THEN
	MONTH(2)=29
	ELSE
	MONTH(2)=28
	END IF
	GO TO 41
	END IF
      MOD=IY-4*(IY/4)
      IF(IY.NE.0.AND.MOD.EQ.0) GO TO 40
      MONTH(2)=28
      GO TO 41
   40 MONTH(2)=29
   41 CONTINUE
   29 WRITE(KPR,21) KSTN,IC,ID,IM,IY,(IHT(L),L=1,12),KSTN,IC,ID,IM,IY
     1,(IHT(L),L=13,24)
   27 CONTINUE
      RETURN
      END
      SUBROUTINE INPUT(IPU,ZOFF,ISTN,N,ICN,ICHK,OBSF)
C
C***********************************************************************
C    INPUT READS IN HOURLY HEIGHT CARDS AND SETS UP DATA ARRAY Z. GAPS
C    IN THE DATA ARE SET TO 1.E35. THE PROCEDURE USED IS SIMILAR TO
C    THAT OF THE ORIGINAL FLEXIBLE ANALYSIS PROGRAM.
C
C  INPUTS
C        FORMAL PARAMETERS -- IPU  - INPUT UNIT NUMBER
C                          -- ZOFF - VALUE TO BE SUBTRACTED FROM DATA
C                          -- ICHK - FLAG TO DENOTE WHETHER OR NOT
C                                    CHECKING OF THE HOURLY HEIGHTS DATA
C                                    CARDS FOR ERRORS IS DESIRED:
C                                    0         = CHECKING
C                                    OTHERWISE = NO CHECKING
C*                         -- OBSF - THE FACTOR THAT WILL MULTIPLY THE
C*                                   INTEGER HOURLY OBSERVATIONS IN
C*                                   ORDER TO PRODUCE THE DESIRED UNITS
C*                                   FOR THE FINAL CONSTITUENT AMPLITUDES
C*                                   AND MINOR SEMI-AXIS LENGTHS.
C                IN COMMON -- IHS,IDS,IMS,IYS,ICS - STARTING TIME OF DATA
C                          -- IHE,IDE,IME,IYE,ICE - ENDING TIME
C                          -- KH1 - TIME COUNT FOR STARTING DATUM
C                                    ( USING SUBROUTINE GDAY)
C  OUTPUTS
C        FORMAL PARAMETERS -- ISTN - STATION NUMBER
C                          -- N - NUMBER OF DATA INCLUDING GAPS
C                          -- ICN - 'N' IF THERE ARE GAPS IN DATA
C                                 - BLANK OTHERWISE.
C                IN COMMON -- Z -THE DATA ARRAY
C
C                                           J. MINAKER  DEC 1973.
C********************************************************************
C
      character*4 kard,kblank,k9999
      character*1 icnn,k1b,icn
      character*48 kard1
      DIMENSION KARD(12),IHT(12)
      COMMON Z(18000),IHS,IDS,IMS,IYS,ICS,IHE,IDE,IME,IYE,
     1ICE,KH1
      equivalence (kard,kard1)
      DATA KBLANK/'    '/,K9999/'9999'/,ICNN/'N'/,K1B/' '/
      I=0
      N=0
      LP=6
      ICN=K1B
C*  I1000 IS USED FOR COMPARISON OF HOURLY HEIGHTS.
      I1000=1000
C
C***********************************************************************
C*  THE HOURLY HEIGHTS DATA CARDS CONTAIN THE FOLLOWING INFORMATION
C*     KOLI,JSTN,IC,ID,IM,IY,(KARD(I),I=1,12)   IN THE FORMAT
C*      (I1,1X,I5,4X,I2,1X,3I2,12A4)
C*     KOLI   = 1 OR 2 INDICATES WHETHER THIS SPECIFIC CARD IS THE FIRST
C*                     SECOND ONE FOR THAT DAY
C*              OTHERWISE  INDICATES A NON DATA CARD WHICH IS IGNORED.
C*     JSTN   = TIDAL STATION NUMBER
C*     IC,ID,IM,IY= CENTURY,DAY, MONTH, &amp; YEAR OF THE HEIGHTS ON THIS CARD
C*     KARD   = THE HOURLY HEIGHTS IN INTEGER FORM. THE FINAL CONSTI-
C*              TUENT AMPLITUDES ARE IN UNITS 1/100 OF THOSE FOR THE
C*              HOURLY HEIGHTS. MISSING VALUES SHOULD BE SPECIFIED AS A
C*              BLANK FIELD OR 9999.
C*     WHEN KOLI=1 THE FIRST HOURLY HEIGHT ON THE DATA CARD IS AT 0100
C*     HR AND WHEN KOLI=2, IT IS AT 1300 HR.
C*     IC=0 WILL BE RESET TO IC=19
C***********************************************************************
C
C  READ UNTIL STARTING DATE IS FOUND
C
10    READ(IPU,200,END=1) KOLI,JSTN,IC,ID,IM,IY,KARD
  200 FORMAT(I1,1X,I5,4X,I2,1X,3I2,12A4)
	IF(IC.EQ.0) IC=19
      IF((KOLI-1)*(KOLI-2)) 10,9,10
    9 IF(IY.NE.IYS) GO TO 10
      IF(IC.NE.ICS) GO TO 10
      IF(IM.NE.IMS) GO TO 10
      IF(ID.NE.IDS) GO TO 10
      L=IHS-12*(KOLI-1)-1
      IF(L.GE.12) GO TO 10
      ISTN=JSTN
       DO 11 K=1,12
      IF(KARD(K).EQ.KBLANK) KARD(K)=K9999
   11 CONTINUE
C
C***********************************************************************
C*  	INTERNAL READ TO CONVERT FORMAT
C
      read(kard1,111,err=123) (IHT(K),K=1,12)
      go to 112
  111 FORMAT(12I4)
  123 WRITE(6,113) KARD1
  113 FORMAT('  error in decoding  kard=  ',
     12X,12A4)
      STOP
  112 CONTINUE
      IHTP=IHT(L+1)
      KHP=KH1-L
C
C***********************************************************************
C*  STOP WHEN THE FINAL DATE IS FOUND.
C
   30 IF(IY.LT.IYE) GO TO 12
      IF(IC.LT.ICE) GO TO 12
      IF(IM.LT.IME) GO TO 12
      IF(ID.LT.IDE) GO TO 12
      IF((KOLI-1)*12 + L .LT. IHE) GO TO 12
      GO TO 13
   12 IF(L.NE.12) GO TO 14
15    READ(IPU,200,END=1) KOLI,JSTN,IC,ID,IM,IY,KARD
	IF(IC.EQ.0) IC=19
C
C***********************************************************************
C*  IF ICHK=0 SEVERAL ERROR CHECKS ARE MADE ON THE DATA INPUT. THESE
C*  INCLUDE
C*   A) COMPARISON OF THE STATION NUMBER READ ON EACH CARD WITH THE
C*      STATION NUMBER OF THE FIRST CARD
C*   B) CHECKING THE DAY, MONTH, YEAR ON EACH CARD BY INSURING THAT
C*      THE CUMULATIVE HOUR NUMBER FOR THE FIRST READING ON EACH CARD
C*      DIFFERS FROM THAT OF THE PREVIOUS CARD BY EXACTLY 12 HOURS.
C*   C) COMPARISON OF SUCCESSIVE HOURLY HEIGHTS TO INSURE THAT THEIR
C*      DIFFERENCE IS NOT LARGER THAN I1000
C*
      IF(ICHK.EQ.0.AND.JSTN.NE.ISTN) GO TO 18
      IF((KOLI-1)*(KOLI-2)) 15,16,15
   16 DO 17 K=1,12
      IF(KARD(K).EQ.KBLANK) KARD(K)=K9999
17    CONTINUE
      read(kard1,111,err=124) (IHT(K),K=1,12)
      GO TO 114
  124 WRITE(6,113) KARD1
      STOP
  114 CONTINUE
      IF(ICHK.NE.0) GO TO 22
      CALL GDAY(ID,IM,IY,IC,KD)
      KH=KD*24+12*(KOLI-1)+1
C
C***********************************************************************
C*  KHP IS THE CONSECUTIVE HOUR NUMBER (CALCULATED IN SUBROUTINE GDAY)
C*  OF THE FIRST HEIGHT ON THE PREVIOUS CARD
C*  KH IS THE CONSECUTIVE HOUR NUMBER OF THE FIRST HEIGHT ON THE PRESENT
C*  CARD.
C*  A CHECK IS MADE TO INSURE THAT KH-KHP=12.
C
      IF(KH-KHP-12) 18,19,18
18    WRITE(LP,300) KOLI,JSTN,IC,ID,IM,IY,KARD
  300 FORMAT(' THIS CARD IN ERROR. GO TO NEXT SET.',10X,I1,I5,3X,4I2,12A
     14)
	write(6,*) ' kh,khp=',kh,khp
      RETURN
   19 KHP=KH
   22 L=0
   14 L=L+1
      IF(IHT(L).EQ.9999) GO TO 20
      IF(IHTP.EQ.9999) GO TO 21
      IF(ICHK.NE.0) GO TO 21
C
C***********************************************************************
C*  EVERY HEIGHT IS COMPARED TO IHTP TO INSURE THAT THE DIFFERENCE DOES
C*  NOT EXCEED I1000.  IN THIS CASE IHTP IS SET EQUAL TO THE PREVIOUS
C*  HEIGHT.
C
      IF(IABS(IHT(L)-IHTP)-I1000) 21,21,18
   21 I=I+1
      Z(I)=IHT(L)*OBSF - ZOFF
      IHTP=IHT(L)
      GO TO 30
   20 I=I+1
      Z(I)=1.E35
      IHTP=IHT(L)
      ICN=ICNN
      GO TO 30
   13 N=I
      RETURN
    1 CONTINUE
      WRITE(LP,301) ISTN
  301 FORMAT(' PERIOD OF DATA TO BE ANALYZED FOR',I5,' WAS NOT FOUND')
      STOP
      END
      SUBROUTINE UCON(NAME,F,KMPR,M,N,NAMEU,FU,MU,RAY)
C
C***********************************************************************
C*    GIVEN A LIST OF M POSSIBLE CONSTITUENTS , THE RAY CRITERION IS
C*    USED TO DETERMINE MU USEABLE CONSTITUENTS. NAME(K),F(K),FC(K)
C*    K=1...M ARE THE 5 CHAR NAME,FREQ IN CYCLES PER TIME INCREMENT,AND
C*    COMPARISON FREQ OF THE INPUT CONSTITS.  NAMEU(K) AND FU(K)  K=1..
C*    .MU ARE THE USEABLE CONSTITS. N IS THE NO. PTS IN THE DATA SERIES.
C*    RAY IS THE RAYLEIGH CRITERION,  UNITY OR PERHAPS LESS.
C*    FOR PROG. G10620   OCEANOGRAPHY/EMR   JUNE/70
C*
      character*5 NAME,NAMEU,KMPR,KBL
      DIMENSION NAME(170),F(170),KMPR(170),NAMEU(100),FU(100)
      DATA KBL/'     '/
      LP=6
      MU=0
      DO 50 K=1,M
      IF(KMPR(K).EQ.KBL) GO TO 50
      DO 51 L=1,M
      IF(NAME(L).EQ.KMPR(K)) GO TO 52
   51 CONTINUE
      WRITE(LP,53) KMPR(K)
   53 FORMAT(1X,'CANNOT FIND THIS CONSTITUENT FOR RAYLEIGH COMPARISON
     1PURPOSES',2X,A5)
      STOP
   52 IF(ABS(F(K)-F(L))*N.LT.RAY) GO TO 50
      MU=MU+1
      FU(MU)=F(K)
      NAMEU(MU)=NAME(K)
50    CONTINUE
      IF(MU.EQ.0) GO TO 150
      IF(MU.EQ.1) RETURN
C
C***********************************************************************
C*    IF TWO NEIGHBOURING CHOSEN CONSTITUENTS VIOLATE THE RAYLEIGH
C*    CRITERION A WARNING IS PRINTED.
C
      DO 100 K=2,MU
      IF(ABS(FU(K)-FU(K-1))*N .GE. RAY) GO TO 100
      WRITE(LP,60) NAMEU(K)
   60 FORMAT(/' RAY VIOLATION ',A5,/)
100   CONTINUE
      RETURN
150   WRITE(LP,160)
160   FORMAT(/ , ' NO USEABLE CONSTITUENTS , STOP',/)
      STOP
      END
      SUBROUTINE VUF (KH,KONX,VUX,FX,XLAT)
C
C***********************************************************************
C*  THIS SUBROUTINE CALCULATES V (ASTRONOMICAL PHASE ARGUMENT), U AND F
C*  (NODAL MODULATION PHASE AND AMPLITUDE CORRECTIONS) FOR ALL CONSTITU-
C*  ENTS.
c*
C*	This October 1992 version also recalculates the constituent
c	frequencies for the middle of the analysis period
C
      character*5 KON,KBLANK,KONCO,KONX,kontab
	real*8 d1,h,pp,s,p,enp,dh,dpp,ds,dp,dnp,hh,tau,dtau
      DIMENSION KON(170),VU(170),F(170),NJ(170),II(50),JJ(50),KK(50),
     1 LL(50),MM(50),NN(50),SEMI(50),kontab(170),freq(170)
      DIMENSION EE(180),LDEL(180),MDEL(180),NDEL(180),IR(180),PH(180)
      DIMENSION KONCO(320),COEF(320),indx(170)
      	common /const/kontab,freq,mtot
	DATA KBLANK/'     '/
C
C***********************************************************************
C*  THE DIMENSION OF KON, VU, F, AND NJ SHOULD BE AT LEAST EQUAL TO THE
C*  TOTAL NUMBER OF POSSIBLE CONSTITUENTS (PRESENTLY 146), THE DIMENSION
C*  OF II, JJ, KK, LL, MM, NN AND SEMI SHOULD BE AT LEAST EQUAL TO THE
C*  NUMBER OF MAIN CONSTITUENTS (PRESENTLY 45), THE DIMENSION OF EE,
C*  LDEL, MDEL, NDEL, IR, AND PH SHOULD BE AT LEAST EQUAL TO THE TOTAL
C*  NUMBER OF SATELLITES TO ALL THE MAIN CONSTITUENTS PLUS THE NUMBER
C*  OF CONSTITUENTS WITH NO SATELLITES (PRESENTLY 162+8),
C*  AND THE DIMENSION OF KONCO, AND COEFF SHOULD BE AT LEAST EQUAL TO
C*  THE SUM FOR ALL SHALLOW WATER CONSTITUENTS OF THE NUMBER OF MAIN
C*  CONSTITUENTS FROM WHICH EACH IS DERIVED (PRESENTLY 251).
C***********************************************************************
C* GIVEN CONSTITUENT KONX , THE NODAL CORRECTIONS V+U AND F ARE RETURNED
C
      DO 20 K=1,NTOTAL
      IF(KON(K).eq.KONX) go to 40
20    CONTINUE
      WRITE(LP,30)KONX
   30 FORMAT(' VUF STOP.',A5)
      STOP
40    VUX=VU(K)
      FX=F(K)
      RETURN
C
C***********************************************************************
C*  THE ASTRONOMICAL ARGUMENTS AND THEIR RATES OF CHANGE,
C*  S0,H0,P0,ENP0,PP0,DS,DH,DP,DNP,DPP,  ARE READ FROM TWO RECORDS IN
C*  THE FORMAT(5F13.10):
C*     S0  = MEAN LONGITUDE OF THE MOON (CYCLES) AT 000 ET 1/1/1976.
C*     H0  = MEAN LONGITUDE OF THE SUN.
C*     P0  = MEAN LONGITUDE OF THE LUNAR PERIGEE.
C*     ENP0= NEGATIVE OF THE MEAN LONGITUDE OF THE ASCENDING NODE.
C*     PP0 = MEAN LONGITUDE OF THE SOLAR PERIGEE (PERIHELION).
C*     DS,DH,DP,DNP,DPP ARE THEIR RESPECTIVE RATES OF CHANGE OVER A 365
C*     DAY PERIOD AS OF 000 ET 1/1/1976.
C
      ENTRY OPNVUF(KH,KONX,VUX,FX,XLAT)
      KR=8
      LP=6
      PI=3.1415926536
      TWOPI=2.*PI
c	These values are no longer used though they are still
c	read in. More accurate polynomial approximations are 
c	now employed.
      READ(KR,50)S0,H0,P0,ENP0,PP0,DS,DH,DP,DNP,DPP
 50   FORMAT(5F13.10)
C
C***********************************************************************
C*  HERE THE MAIN CONSTITUENTS AND THEIR DOODSON NUMBERS ARE READ IN
C*  FORMAT (6X,A5,1X,6I3,F5.2,I4). THE VALUES ARE RESPECTIVELY
C*     KON    = CONSTITUENT NAME
C*  II,JJ,KK,LL,MM,NN = THE SIX DOODSON NUMBERS
C*     SEMI   = PHASE CORRECTION
C*     NJ     = THE NUMBER OF SATELLITES FOR THIS CONSTITUENT.
C*  THE END OF ALL MAIN CONSTITUENTS IS DENOTED BY A BLANK CARD.
C
      JBASE=0
      DO 90 K=1,1000
      READ(KR,60)KON(K),II(K),JJ(K),KK(K),LL(K),MM(K),NN(K),SEMI(K),
     2 NJ(K)
   60 FORMAT(6X,A5,1X,6I3,F5.2,I4)
      IF(KON(K).eq.KBLANK) go to 100
70    J1=JBASE+1
      IF(NJ(K).GE.1) GO TO 75
      NJ(K)=1
      JL=J1
      PH(J1)=0.
      EE(J1)=0.
      LDEL(J1)=0
      MDEL(J1)=0
      NDEL(J1)=0
      IR(J1)=0
      GO TO 90
   75 JL=JBASE+NJ(K)
C
C***********************************************************************
C*  IF NJ&gt;0, INFORMATION ON THE SATELLITE CONSTITUENTS IS READ , THREE
C*  SATELLITES PER CARD, IN THE FORMAT (11X,3(3I3,F4.2,F7.4,1X,I1,1X)).
C*  FOR EACH SATELLITE THE VALUES READ ARE
C*     LDEL,MDEL,NDEL = THE CHANGES IN THE LAST THREE DOODSON NUMBERS
C*                      FROM THOSE OF THE MAIN CONSTITUENT.
C*     PH     = THE PHASE CORRECTION
C*     EE     = THE AMPLITUDE RATIO OF THE SATELLITE TIDAL POTENTIAL TO
C*              THAT OF THE MAIN CONSTITUENT.
C*     IR     = 1 IF THE AMPLITUDE RATIO HAS TO BE MULTIPLIED BY THE
C*                LATITUDE CORRECTION FACTOR FOR DIURNAL CONSTITUENTS
C*              2 IF THE AMPLITUDE RATIO HAS TO BE MULTIPLIED BY THE
C*                LATITUDE CORRECTION FACTOR FOR SEMI-DIURNAL CONSTI-
C*                TUENTS.
C*              OTHERWISE IF NO CORRECTION IS REQUIRED TO THE AMPLITUDE
C*                RATIO.
C
      READ(KR,80)(LDEL(J),MDEL(J),NDEL(J),PH(J),EE(J),IR(J),J=J1,JL)
   80 FORMAT((11X,3(3I3,F4.2,F7.4,1X,I1,1X)))
90    JBASE=JL
100   NTIDAL=K-1
C
C***********************************************************************
C*  THE SHALLOW WATER CONSTITUENTS AND THE MAIN CONSTITUENTS FROM WHICH
C*  THEY ARE DERIVED ARE READ IN HERE WITH THE FORMAT
C*  (6X,A5,I1,2X,4(F5.2,A5,5X)). THE VALUES ARE RESPECTIVELY
C*     KON    = NAME  OF THE SHALLOW WATER CONSTITUENT
C*     NJ     = NUMBER OF MAIN CONSTITUENTS FROM WHICH IT IS DERIVED.
C*     COEF,KONCO = COMBINATION NUMBER AND NAME OF THESE MAIN
C*                  CONSTITUENTS.
C*  THE END OF THESE CONSTITUENTS IS DENOTED BY A BLANK CARD.
C
      JBASE=0
      K1=NTIDAL+1
      DO 160 K=K1,1000
      J1=JBASE+1
      J4=J1+3
      READ(KR,130)KON(K),NJ(K),(COEF(J),KONCO(J),J=J1,J4)
  130 FORMAT(6X,A5,I1,2X,4(F5.2,A5,5X))
      IF(KON(K).eq.KBLANK) go to 170
160   JBASE=JBASE+NJ(K)
170   NTOTAL=K-1
      RETURN
C
C***********************************************************************
C*  NTIDAL IS THE NUMBER OF MAIN CONSTITUENTS
C*  NTOTAL IS THE NUMBER OF CONSTITUENTS (MAIN + SHALLOW WATER)
C*  FOR  THE GIVEN TIME KH, THE TABLE OF F AND V+U VALUES IS
C*  CALCULATED FOR ALL THE CONSTITUENTS.
C*     F IS THE NODAL MODULATION ADJUSTMENT FACTOR FOR AMPLITUDE
C*     U IS THE NODAL MODULATION ADJUSTMENT FACTOR FOR PHASE
C*     V IS THE ASTRONOMICAL ARGUMENT ADJUSTMENT FOR PHASE.
C
      ENTRY SETVUF(KH,KONX,VUX,FX,XLAT)
      SLAT=SIN(PI*XLAT/180.)
c      CALL GDAY(1,1,76,19,KD)
c      YEARS=(KH/24.D0-KD)/365.00D0
C
C***********************************************************************
C*  THE ASTRONOMICAL ARGUMENTS ARE CALCULATED BY LINEAR APPROXIMATION
C*  AT THE MID POINT OF THE ANALYSIS PERIOD.
C
c      S=S0+YEARS*DS
c      H=H0+YEARS*DH
c      P=P0+YEARS*DP
c      ENP=ENP0+YEARS*DNP
c      PP=PP0+YEARS*DPP
c	day number measured from January 0.5 1900 (i.e.,
c	1200 UT December 31, 1899
	d1=kh/24.d0
	call gday(31,12,99,18,kd0)
	d1=d1-dfloat(kd0)-0.5d0
	call astr(d1,h,pp,s,p,enp,dh,dpp,ds,dp,dnp)
      INT24=24
      INTDYS=KH/INT24
      HH=dfloat(KH-INTDYS*INT24)
      TAU=HH/24.D0+H-S
	dtau=365.d0+dh-ds
C
C***********************************************************************
C*  ONLY THE FRACTIONAL PART OF A SOLAR DAY NEED BE RETAINED FOR COMPU-
C*  TING THE LUNAR TIME TAU.
C
      JBASE=0
      DO 210 K=1,NTIDAL
	do 209 l=1,mtot
	if(kon(k).eq.kontab(l)) then
      FREQ(l)=(II(K)*DTAU+JJ(K)*DS+KK(K)*DH+LL(K)*DP+MM(K)*DNP+
     1NN(K)*DPP)/(365.*24.)
	indx(k)=l
	end if
209	continue
      VDBL=II(K)*TAU+JJ(K)*S+KK(K)*H+LL(K)*P+MM(K)*ENP+NN(K)*PP+SEMI(K)
      IV=VDBL
      IV=(IV/2)*2
      V=VDBL-IV
      J1=JBASE+1
      JL=JBASE+NJ(K)
      SUMC=1.
      SUMS=0.
      DO 200 J=J1,JL
C
C***********************************************************************
C*  HERE THE SATELLITE AMPLITUDE RATIO ADJUSTMENT FOR LATITUDE IS MADE
C
      RR=EE(J)
      L=IR(J)+1
      GO TO (901,902,903),L
  902 RR=EE(J)*0.36309*(1.-5.*SLAT*SLAT)/SLAT
      GO TO 901
  903 RR=EE(J)*2.59808*SLAT
  901 CONTINUE
      UUDBL=LDEL(J)*P+MDEL(J)*ENP+NDEL(J)*PP+PH(J)
      IUU=UUDBL
      UU=UUDBL-IUU
      SUMC=SUMC+RR*COS(UU*TWOPI)
      SUMS=SUMS+RR*SIN(UU*TWOPI)
  200 CONTINUE
      F(K)=SQRT(SUMC*SUMC+SUMS*SUMS)
      VU(K)=V+ATAN2(SUMS,SUMC)/TWOPI
210   JBASE=JL
C
C***********************************************************************
C*  HERE F AND V+U OF THE SHALLOW WATER CONSTITUENTS ARE COMPUTED FROM
C*  THE VALUES OF THE MAIN CONSTITUENT FROM WHICH THEY ARE DERIVED.
C
      JBASE=0
      K1=NTIDAL+1
      IF(K1.GT.NTOTAL) RETURN
      DO 270 K=K1,NTOTAL
      F(K)=1.0
      VU(K)=0.0
	iflag=0
	do 269 lk=1,mtot
	if(kon(k).eq.kontab(lk)) then
      FREQ(lk)=0.
	iflag=1
	go to 268
	end if
269	continue
268   J1=JBASE+1
      JL=JBASE+NJ(K)
      DO 260 J=J1,JL
      KM1=K-1
      DO 240 L=1,KM1
      IF(KON(L).eq.KONCO(J)) go to 250
240   CONTINUE
      WRITE(LP,241)KONCO(J)
  241 FORMAT('   SETVUF STOP.',A5)
      STOP
250   F(K)=F(K)*F(L)**ABS(COEF(J))
      VU(K)=VU(K)+COEF(J)*VU(L)
      if(iflag.eq.1) FREQ(lk)=FREQ(lk)+COEF(J)*FREQ(indx(L))
260	continue
270   JBASE=JL
      RETURN
      END
      SUBROUTINE INFER(KON,M,N,KONA,AAN,EPSAN,KONI,AIN,EPSIN,SIGI,
     1NIN)
C
C***********************************************************************
C*  THIS SUBROUTINE INFERS THE AMPLITUDE &amp; PHASE OF A CONSTITUENT NOT
C*  INCLUDED IN THE ANALYSIS FROM ONE THAT WAS. THE AMPLITUDE &amp; PHASE OF
C*  THE CONSTITUENT USED FOR THE INFERENCE IS APPROPRIATELY ADJUSTED.
C
C*     KONAN &amp; SIGAN ARE THE NAME AND FREQUENCY OF A CONSTITUENT TO BE
C*                   USED FOR INFERENCE.
C*     KONIN &amp; SIGIN ARE THE NAME AND FREQUNCY OF A CONSTITUENT TO BE
C*                   INFERRED FROM KONAN.
C*     R IS THE AMPLITUDE RATIO OF KONIN TO KONAN.
C*     ZETA IS THE GREENWICH PHASE LAG OF THE MAIN CONSTITUENT -
C*          GREENWICH PHASE LAG OF THE INFERRED CONSTITUENT.
C*  KONAN,SIGAN,KONIN,SIGIN,R,ZETA ARE READ FROM DEVICE MTD WITH THE
C*  FORMAT (2(4X,A5,E16.10),2F10.3). THEIR END IS DENOTED BY A BLANK
C*  CARD.
C
      character*5 KONAN,KONIN,KBLANK,KON,KONI,KONA
      DIMENSION KON(100),KONAN(20),KONIN(20),R(20),ZETA(20)
      DIMENSION SIGAN(20),SIGIN(20)
      DATA KBLANK/'     '/
C
C***********************************************************************
C*  TEST FOR INFERENCE
C*  IF A CONSTITUENT SPECIFIED FOR INFERENCE WAS INCLUDED DIRECTLY IN
C*  THE LEAST SQUARES FIT ANALYSIS, THEN THE INFERENCE IS IGNORED.
C
      DO 30 L=1,NIN
      IF(KONA.ne.KONAN(L)) go to 30
10    DO 20 KK=1,M
      IF(KONIN(L).eq.KON(KK)) go to 30
20    CONTINUE
      GO TO 40
30    CONTINUE
      KONI=KBLANK
      RETURN
C
C***********************************************************************
C*  PERFORM INFERENCE
C
40    KONI  =KONIN(L)
      SIGI  =SIGIN(L)
      ZETAC=ZETA(L)/360.
      CALL VUF (KH,KONAN(L),VUAN,FAN,XLAT)
      CALL VUF (KH,KONIN(L),VUIN,FIN,XLAT)
      SIGDIF=SIGAN(L)-SIGIN(L)
      DSIGIA=SIGDIF*N*PI
      IBANG=SIGDIF*N*.5D0
      BANG=(SIGDIF*N*.5D0-IBANG)*TWOPI
      V=SIN(BANG)/DSIGIA
      FACTOR=R(L)*V*FIN/FAN
      ETA=(VUIN-VUAN+ZETAC)*TWOPI
      STERM=FACTOR*SIN(ETA)
      CTERM=1.+FACTOR*COS(ETA)
      DG=ATAN2(STERM,CTERM)/TWOPI
      DIV=SQRT(CTERM*CTERM+STERM*STERM)
      EPSAN=EPSAN+DG
      AAN=AAN/DIV
      EPSIN=EPSAN+VUAN-ZETAC-VUIN
      AIN=(AAN/FAN)*R(L)*FIN
      RETURN
C
C***********************************************************************
C*  READ INFERENCE DATA.
C
      ENTRY OPNINF(KON,M,N,KONA,AAN,EPSAN,KONI,AIN,EPSIN,SIGI,NIN)
      MTD=4
      PI=3.1415926536
      TWOPI=2.*PI
      DO 1020 K=1,100
      READ(MTD,1010)KONAN(K),SIGAN(K),KONIN(K),SIGIN(K),R(K),ZETA(K)
 1010 FORMAT(2(4X,A5,E16.10),2F10.3)
      IF(KONAN(K).eq.KBLANK) go to 1040
1020  CONTINUE
1040  NIN=K-1
      RETURN
      END
      SUBROUTINE SCFIT2(X,NIN,F,M,IND,C,S,ERC,ERS,G,NX,AVREM,SDX,RT,RA,X
     1P)
C     ------------------------------------------------------------------
C        THIS SUBROUTINE FINDS THE LEAST SQUARES FIT TO AN EQUALLY
C     SPACED TIME SERIES USING SINES AND COSINES OF SPECIFIED FREQ-
C     UENCIES AS FITTING FUNCTIONS. THERE MAY BE GAPS IN THE DATA SERIES
C
C  INPUTS
C  ------
C   --X(I),I=1...NIN  THE DATA SERIES TO BE FITTED. IF THERE ARE GAPS IN
C     THE DATA , THE X VALUES IN THE GAPS SHOULD BE SET TO 1.E35 .
C   --NIN = THE NUMBER OF POINTS IN SERIES X INCLUDING GAPS .
C     NIN SHOULD BE AN ODD NUMBER . IF IT IS NOT THE LAST X VALUE
C     WILL BE COMPLETELY IGNORED BY THE ANALYSIS .
C   --F(K),K=1...M  THE FREQUENCIES OF THE SIN AND COS COMPONENTS USED
C     IN THE ANALYSIS IN CYCLES PER TIME INCREMENT. IF THE ZERO
C     FREQUENCY IS USED IT MUST BE THE FIRST OF THE SET (IE F(1)=0.0)
C   --M = NUMBER OF FREQUENCIES IN THE ANALYSIS.
C   --IND = 0 IF THE CALCULATION OF RA AND XP IS TO BE BYPASSED.
C         = 1 IF RA AND XP ARE TO BE CALCULATED.
C         = 2 IF RA AND XP ONLY ARE TO BE CALCULATED FROM THE INPUTS X,
C                NIN,F,M,C, AND S. (IE ANALYSIS IS BYPASSED AND
C                PREDICTIONS ARE MADE FROM GIVEN C AND S ARRAYS.)
C
C  OUTPUTS
C  -------
C   --C(K),S(K),K=1...M ARE THE AMPLITUDES OF THE COS AND SIN COMPONENTS
C     OF THE FIT ...
C   --ERC(K),ERS(K),K=1...M ARE THE EXPECTED ERRORS IN C AND S .
C   --G IS THE CONDITION OF THE MATRIX OF NORMAL EQUATIONS .
C       = 1.0 FOR A PERFECT DIAGONAL MATRIX .
C       = 0.0 FOR A SINGULAR MATRIX (IE NO SOLUTION POSSIBLE)
C   --NX,AVX,SDX ARE THE NUMBER OF VALID DATA POINTS IN SERIES X (IE
C     WITH THOSE = 1.E35 OMITTED) , AND THE AVERAGE AND STANDARD DEV-
C     IATION OF X .
C   --RT IS THE RMS OF THE RESIDUALS OF THE FIT CALCULATED PREVIOUS TO
C     THE PREDICTIONS USING THE MULTIPLE CORRELATION TECHNIQUE.
C   --RA IS THE SAME AS RT ONLY CALCULATED DIRECTLY FROM THE DIFFERENCES
C     BETWEEN DATA AND PREDICTIONS. IF RA AND RT ARE NOT PRETTY CLOSE
C     THEN THERE IS A BUG IN THIS PROGRAM OR ROUNDING ERRORS ARE GETTING
C     SERIOUS OR THE FIT IS ALMOST PERFECT.
C   --XP(I),I=1,..NIN ARE THE PREDICTED X VALUES.
C     XP(I) = C(K)*COS(2PI*F(K)*T) + S(K)*SIN(2PI*F(K)*T)  SUMMED OVER M
C     T=I-NIN/2 (IE THE TIME ORIG IS AT THE CENTER POINT OF SERIES X .)
C
C  NOTES
C  ------
C   1) AVX IS SUBTRACTED FROM X DURING THE ANALYSIS IF F(1) = 0.
C   2) ALL FREQUENCIES SHOULD BE LESS THAN  HALF A CYCLE PER TIME
C     INCREMENT . FOR GOOD RESOLUTION IT IS SUFFICIENT THAT ADJACENT
C     FREQUENCIES DIFFER BY ONE CYCLE OR MORE OVER THE DURATION OF THE
C     ANALYSIS IF THE GAPS ARE SMALL .
C   3) SUBROUTUNE LSQSLV IS REQUIRED TO SOLVE THE EQUATIONS.
C   4) ARRAY A MUST BE DIMENSIONED AT LEAST (2*M+1)*M , RHS AT LEAST 2*M
C      AND THE OTHER ARRAYS AT LEAST M.
C
C     THIS ROUTINE IS PART OF THE TIDAL STREAMS ANALYSIS PROGRAM FOR
C     DR G GODIN    OCEANOGRAPHIC RESEARCH/EMR    JULY/70
C
      REAL*8 AVX,SSX,CWTK,CWTKK,SWTK,CK,SK,CWK,SWK,XL,XU,XPDT,RAA,CW1,SW
     11,CSUM,SSUM
      DIMENSION F(100),ERS(100),ERC(100),C(100),S(100)
      DIMENSION RHS(200),CW(100),SW(100),CW1(100),SW1(100)
      DIMENSION X(18000),XP(18000),A(15000)
      DIMENSION AC(7500),AS(7500),RHSC(100),RHSS(100)
      EQUIVALENCE (AC(1),A(1)),(AS(1),A(7501)),(RHS(1),RHSC(1)),(RHS(101
     1),RHSS(1))
C
C***********************************************************************
C*  INITIALIZE
C
      PI=3.141592653589793
      PI2 = 2.*PI
      BIG=.9E35
      LP = 6
      IMID = (NIN+1)/2
      N =  IMID*2-1
      NH =N/2
      NH1=NH+1
C
C***********************************************************************
C*  NX IS THE NUMBER OF OBSERVATIONS (NOT INCLUDING GAPS) IN THE
C*     DESIRED ANALYSIS PERIOD.
C*  AVX=AVREM IS THE AVERAGE OF THESE OBSERVATIONS.
C*  SDX IS THE STANDARD DEVIATION ESTIMATE.
C
      NX=0
      AVX=0.D0
      DO 25 I=1,N
      IF(X(I).GE.BIG) GO TO 25
      NX=NX+1
      AVX=AVX+X(I)
25    CONTINUE
      AVX=AVX/NX
      SSX = 0.D0
      DO 27 I=1,N
      IF(X(I).LT.BIG) SSX=SSX+(X(I)-AVX)**2
27    CONTINUE
      SDX=SSX/(NX-1)
      SDX=SQRT(SDX)
C
C***********************************************************************
C*  SSX IS THE ORIGINAL SUM OF SQUARES OF THE HOURLY HEIGHTS.
C*  NDF IS THE NUMBER OF DEGREES OF FREEDOM.
C
      NDF=NX-2*M+1
      IF(NDF.GE.0) GO TO  40
      WRITE (LP,30)
30    FORMAT(/' SUBROUTINE SCFIT2 , NDF NEGATIVE , STOP' /)
      STOP
C
C***********************************************************************
C*  THE ZERO FREQUENCY MUST BE THE FIRST ONE IN THE LIST.
C
40    DO  50 K=1,M
      CW(K) = COS(PI2*F(K))
50    SW(K) = SIN(PI2*F(K))
      M2=2*M
      M2M2=(M2*M2+M2)/2
      M1M1=(3*M*M+M)/2+1
      MM=(M*M+M)/2
      IF(IND.EQ.2) GO TO 500
C
C***********************************************************************
C*  ACCUMULATE RIGHT SIDES OF NORMAL EQUATIONS FOR C AND S.
C*  THESE TAKE THE FORM OF SUM(I) Y(I)*COS(2PI*SIGMA(J)*T(I))  AND
C*  SUM(I) Y(I)*SIN(2PI*SIGMA(J)*T(I)) FOR J=1,M CONSTITUENTS AND
C*  T(I)=-NH,-NH+1,...,0,...NH.
C
C
      DO 140 K=2,M
      CWK = CW(K)
      SWK = SW(K)
      CWTK =1.D0
      SWTK =0.D0
      CK=0.D0
      IF(X(IMID).LT.BIG) CK=X(IMID)-AVX
      SK = 0.D0
      DO 120  I=1,NH
C
C***********************************************************************
C*  USING THE IDENTITIES FOR COS(T+DT) AND SIN(T+DT)
C
      CWTKK = CWTK*CWK - SWTK*SWK
      SWTK  = CWTK*SWK + SWTK*CWK
      CWTK=CWTKK
      IL =IMID-I
      IU= IMID+I
      XL=0.D0
      XU=0.D0
      IF(X(IL).LT.BIG) XL=X(IL)-AVX
      IF(X(IU).LT.BIG) XU=X(IU)-AVX
C
C***********************************************************************
C*  USING COS(DT)=COS(-DT) AND SIN(DT)=-SIN(-DT)
C
      CK = CK + (XU+XL)*CWTK
120   SK = SK + (XU-XL)*SWTK
      C(K)=CK
      S(K)=SK
140   CONTINUE
C
C***********************************************************************
C*  BECAUSE THE AVERAGE VALUE HAS BEEN SUBTRACTED FROM ALL THE OBSERVA-
C*  TIONS, THE FIRST ELEMENT IN THE RIGHT HAND SIDE IS 0.
C
      C(1)=0.
      S(1)=0.
C
C***********************************************************************
C*  IF THERE ARE NO GAPS IN THE DATA THEN THE RESULTANT LEAST SQUARES
C*  MATRIX HAS ZEROS IN THE UPPER RIGHT AND LOWER LEFT PARTITIONS. IF
C*  THE NEW MATRIX EQUATIONS OBTAINED FROM THESE NON-ZERO PARTITIONS
C*  ARE SOLVED SEPARATELY, THIS MAKES FOR AN APPROXIMATE FOUR FACTOR
C*  TIME SAVING IN THE CHOLESKY FACTORIZATION.
C
      IF(N.EQ.NX) GO TO 600
C
C***********************************************************************
C*  GENERATE THE MATRIX BY SUMMING THE CONTRIBUTIONS FROM EACH OF THE
C*  CONTINUOUS BLOCKS OF DATA.
C
      DO 160 JK=1,M2M2
160   A(JK)=0.0
C
C***********************************************************************
C*  IA AND IB ARE RESPECTIVELY THE FIRST AND LAST READINGS IN A
C*  CONTINUOUS BLOCK OF DATA.
C
      IA=-NH-1
180   IA=IA+1
      IF(IA.GT.NH) GO TO 300
      IIA=IA+IMID
      IF(X(IIA).GE.BIG) GO TO 180
      I=IA
185   I=I+1
      IF(I.GT.NH) GO TO 190
      II=I+IMID
      IF(X(II).LT.BIG) GO TO 185
190   IB=I-1
      AB=IA+IB
      BA1=IB-IA+1
C
C***********************************************************************
C*  TRIGONOMETRIC IDENTITIES AVOID THE SUMMATIONS REQUIRED IN
C*  CALCULATING THE MATRIX ELEMENTS.
C*  ONLY THE LOWER TRIANGLE (INCLUDING DIAGONAL) OF THE MATRIX NEED BE
C*  GENERATED.
C
      DO 260 J=1,M
      WJ=PI*F(J)
      SIND=BA1
      SINS=BA1
      IF(J.GT.1) SINS=SIN(2.*BA1*WJ)/SIN(2.*WJ)
240   DO 255 K=J,M
      JK=M2*(J-1)+K-(J*J-J)/2
      JKM=JK+M
      JM=J+M
      KJM=M2*(K-1)+JM-(K*K-K)/2
      JMKM=M2*(JM-1)+K+M-(JM*JM-JM)/2
      WK=PI*F(K)
      WD=WJ-WK
      WS=WJ+WK
      IF(J.EQ.K) GO TO 250
      SIND=SIN(BA1*WD)/SIN(WD)
      SINS=SIN(BA1*WS)/SIN(WS)
250   TD=COS(AB*WD)*SIND
      TS=COS(AB*WS)*SINS
      A(JK)=A(JK)+.5*(TD+TS)
      A(JMKM)=A(JMKM)+.5*(TD-TS)
      TD=SIN(AB*WD)*SIND
      TS=SIN(AB*WS)*SINS
      IF(J.EQ.K) GO TO 255
      A(JKM)=A(JKM)+.5*(-TD+TS)
255   A(KJM)=A(KJM)+.5*(TD+TS)
260   CONTINUE
      IA=IB
      GO TO 180
C
C***********************************************************************
C*  S(1) IS FORCED TO ZERO BY SETTING THIS MATRIX ELEMENT TO LARGE VALUE
C
  300 A(M1M1)=NX
C
C***********************************************************************
C*  SOLVE THE EQUATIONS FOR C AND S .
C
      DO 370 K=1,M
      RHS(K)=C(K)
      MPK=M+K
370   RHS(MPK)=S(K)
C
C***********************************************************************
C*  SINCE THE TWO DIMENSIONAL MATRIX DUE TO THE LEAST SQUARES FIT IS
C*  POSITIVE DEFINITE SYMMETRIC, IT CAN BE DECOMPOSED INTO THE FORM
C*  G*G(TRANSPOSE), WHERE G IS A LOWER TRIANGULAR MATRIX. SUBROUTINE
C*  TRIANG FINDS THIS G AND SOLUTN SOLVES THE RESULTING LINEAR
C*  EQUATIONS BY FORWARD AND BACKWARD SUBSTITUTION.
C
      CALL TRIANG(A,RHS,G,M2)
      IF(G.GT.0.) GO TO 371
      WRITE(LP,372) g
  372 FORMAT(' THE LEAST SQUARES MATRIX IS ILL-CONDITIONED AND COULD NOT
     1 BE TRIANGULARIZED: g=',f10.5)
      STOP
  371 CONTINUE
      GG=1.005
      CALL SOLUTN(A,RHS,GG,M2)
      XPDT=0.D0
      DO 380 K=1,M
      MPK=M+K
      XPDT=XPDT+RHS(K)*C(K)+RHS(MPK)*S(K)
      C(K)=RHS(K)
380   S(K)=RHS(MPK)
C
C***********************************************************************
C*  THE EXPECTED ERRORS ARE CALCULATED FROM THE INVERSE MATRIX DIAGONAL
C*  ELEMENTS.
C
      DO 420 K=1,M2
      DO 400 KK=1,M2
400   RHS(KK)=0.
      RHS(K)=1.0
      GG=K+.005
      CALL SOLUTN(A,RHS,GG,M2)
      IF(K.LE.M) GO TO 405
      KMM=K-M
      ERS(KMM)=SQRT(RHS(K))
      GO TO 406
405   ERC(K)=SQRT(RHS(K))
406   CONTINUE
420   CONTINUE
C
C***********************************************************************
C*  GET THEORETICAL RESIDUAL RT AND EXPECTED ERRORS ERC,ERS OF C,S .
C*  XPDT IS THE SUM OF SQUARES DUE TO REGRESSION.
C*  RT IS THE ROOT MEAN SQUARE RESIDUAL ERROR.
C*  SSX-XPDT IS THE RESIDUAL ERROR SUM OF SQUARES.
  515 RAD=SSX-XPDT
      RAD=ABS(RAD)/NDF
      RT=SQRT(RAD)
      DO 450 K=1,M
      ERC(K) =  ERC(K)*RT
450   ERS(K) =  ERS(K)*RT
      C(1)=C(1)+AVX
      AVREM=AVX
C
C***********************************************************************
C*    CALCULATE PREDICTIONS XP(1...N) AND ACTUAL RESIDUAL RA.
C
  500 IF(IND.EQ.0) RETURN
      XP(IMID)=0.
      DO 510 K=1,M
      CW1(K)=1.D0
      SW1(K)=0.D0
  510 XP(IMID)=XP(IMID)+C(K)
      IMAX=NH
      IF(NIN.GT.N) IMAX=NH1
      DO 550 I=1,IMAX
      IL=IMID-I
      IU=IMID+I
      CSUM=0.D0
      SSUM=0.D0
      DO 530 K=1,M
      CWTK=CW1(K)*CW(K)-SW1(K)*SW(K)
      SW1(K)=CW1(K)*SW(K)+SW1(K)*CW(K)
      CW1(K)=CWTK
      CSUM=CSUM+C(K)*CW1(K)
  530 SSUM=SSUM+S(K)*SW1(K)
      IF(I.EQ.NH1) GO TO 531
      XP(IL)=CSUM-SSUM
  531 XP(IU)=CSUM+SSUM
  550 CONTINUE
      RAA=0.D0
      DO 570 I=1,N
      IF(X(I).LT.BIG) RAA=RAA+(X(I)-XP(I))**2
  570 CONTINUE
      RA=RAA
      RA=SQRT(RA/NDF)
      RETURN
C
C***********************************************************************
C*  WHEN THERE ARE NO GAPS IN THE RECORD, THE C AND S COEFFICIENT
C*  MATRICES ARE SOLVED SEPARATELY.
C
  600 BA1=2*NH+1
      DO 601 J=1,MM
      AC(J)=0.
  601 AS(J)=0.
      DO 660 J=1,M
      WJ=PI*F(J)
      T1=BA1
      T2=BA1
      IF(J.GT.1) T2=SIN(2.*BA1*WJ)/SIN(2.*WJ)
      JK=M*(J-1)-(J*J-J)/2+J
      DO 655 K=J,M
      IF(J.EQ.K) GO TO 650
      WK=PI*F(K)
      T1=SIN(BA1*(WJ-WK))/SIN(WJ-WK)
      T2=SIN(BA1*(WJ+WK))/SIN(WJ+WK)
  650 AC(JK)=(T1+T2)*0.5
      AS(JK)=(T1-T2)*0.5
  655 JK=JK+1
  660 CONTINUE
      AS(1)=BA1
      DO 670 I=1,M
      RHSC(I)=C(I)
  670 RHSS(I)=S(I)
      CALL TRIANG(AC,RHSC,GC,M)
      IF(GC.GT.0.) GO TO 609
      WRITE(LP,372)
      STOP
  609 CALL TRIANG(AS,RHSS,GS,M)
      IF(GS.GT.0.) GO TO 611
      WRITE(LP,372)
      STOP
  611 G=GS*GC
      GGS=1.005
      GGC=1.005
      CALL SOLUTN(AC,RHSC,GGC,M)
      CALL SOLUTN(AS,RHSS,GGS,M)
      XPDT=0.D0
      DO 612 K=1,M
      XPDT=XPDT+RHSC(K)*C(K)+RHSS(K)*S(K)
      C(K)=RHSC(K)
  612 S(K)=RHSS(K)
C
C***********************************************************************
C*  THE EXPECTED ERRORS ARE CALCULATED FROM THE INVERSE MATRIX DIAGONAL
C*  ELEMENTS.
C
      DO 613 K=1,M
      DO 614 KK=1,M
      RHSS(KK)=0.
  614 RHSC(KK)=0.
      RHSC(K)=1.
      GGC=K+.005
      CALL SOLUTN(AC,RHSC,GGC,M)
      ERC(K)=SQRT(RHSC(K))
      RHSS(K)=1.
      GGS=K+.005
      CALL SOLUTN(AS,RHSS,GGS,M)
      ERS(K)=SQRT(RHSS(K))
  613 CONTINUE
      GO TO 515
      END
      SUBROUTINE CHLSKY(A,F,G,N)
C
C***********************************************************************
C*    ROUTINE TO SOLVE SYMMETRIC POSITIVE DEFINITE MATRICES FOUND IN
C*    LINEAR LEAST SQUARES PROBLEMS.  THE CHOLESKY SQUARE METHOD (ALSO
C*    ATTRIBUTED TO BANACHIEWICZ) IS USED.  SEE LEAST SQUARES PROGRAM-
C*    MING FOR BAND MATRICES BY B. EVANS. COMPUTATIONAL METHODS FOR
C*    LINEAR ALGEBRA BY FADDEEV AND FADDEEVA PAGE 145. ADVANCES IN
C*    COMPUTERS VOL 2 PAGE 57.  COMPUTER SOLUTION OF LINEAR ALGEBRAIC
C*    SYSTEMS BY FORSYTHE AND MOLER PAGE 114.
C*
C*    DIRECTIONS...
C*    1... CALL TRIANG WITH THE UPPER TRIANGLE OF THE MATRIX STORED
C*    BY ROWS IN THE ONE DIMENSIONAL ARRAR A.  THE PRICE OF THIS
C*    STORAGE SAVING IS EXTRA PROCESSING TIME DUE TO A COMPLEX
C*    SUBSCRIPTING. IN TRIANG, A IS FACTORIZED AND THE NEW MATRIX
C*    REPLACES A.  THE MATRIX CONDITION IS RETURNED IN G.  IF (G.LE.0),
C*    THEN G IS SET TO -I (ROW NUMBER) AND AN IMMEDIATE RETURN IS MADE.
C*    2...CALL SOLUTN WITH THE RIGHT HAND SIDE ARRAY IN F. THE SOLUTION
C*    IS RETURNED IN F.
C
C*    DEC/76 ... INDEXING SIMPLIFIED TO IMPROVE SPEED.
C*    IF F(I)=0 FOR ALL I EXCEPT F(IENTRY)=1.0, THEN SETTING G TO
C*    IENTRY CAUSES THE PROGRAM TO CALCULATE ONLY THE PARTIAL SOLUTION
C*    F(IENTRY)...F(N). THIS IS USEFUL FOR GETTING THE
C*    DIAGONAL ELEMENTS OF THE INVERSE MATRIX WHICH ARE RELATED TO THE
C*    EXPECTED ERRORS IN THE REGRESSION COEFFICIENTS AND ALLOWS A SPEED
C*    IMPROVEMENT OF ABOUT 3X. FOR OTHER TYPES OF RHS THE FULL SOLUTION
C*    IS FOUND. TO CANCEL THIS FEATURE SET IENTRY=1 FOR ALL TIME.
C
C*    TIMING FOR...TRIANGULARIZE + SOLVE ONE RHS + GET INVERSE MATRIX
C*    DIAGONAL ELEMENTS (N=124, CYBER/74)
C*       OLD TIME = 3.0+.09+12.0=15.9 SEC.
C*       NEW TIME = 2.0+.06+ 3.5= 5.56 SEC.=37 PERCENT OF OLD.
C
      DIMENSION A(15000),F(200)
C
C***********************************************************************
C*    CHOLESKY FACTORIZATION OF THE MATRIX A.
      ENTRY TRIANG(A,F,G,N)
      NP1=N+1
10    IF(A(1))20,20,30
20    G=-1.
      RETURN
30    A(1)= SQRT(A(1))
      DO 40 J=2,N
40    A(J)=A(J)/A(1)
      G=1.
      II=1
50    DO 100 I=2,N
      IP1=I+1
      IM1=I-1
      II=II+N+2-I
      AII=A(II)
      LI=I-N
      DO 60 L=1,IM1
      LI=LI+NP1-L
60    AII=AII-A(LI)*A(LI)
      IF(AII)65,65,70
65    G=-I
      RETURN
70    G=G*AII/A(II)
      A(II)= SQRT(AII)
      IF(I-N)75,100,75
75    DO 90 J=IP1,N
      IJ=II+J-I
      AIJ=A(IJ)
      JMI=J-I
      LI=I-N
      DO 80 L=1,IM1
      LI=LI+NP1-L
      LJ=LI+JMI
80    AIJ=AIJ-A(LI)*A(LJ)
90    A(IJ)=AIJ/A(II)
100   CONTINUE
      G= SQRT(G)
      RETURN
C
C***********************************************************************
C*    SOLUTION OF THE TRIANGULARIZED MATRIX EQUATION
C*    G NOW DENOTES WHETHER OR NOT AN EXPECTED ERROR CALCULATION IS
C*    DESIRED; THAT IS, IF THE RIGHT HAND SIDE OF THE MATRIX EQUATION
C*    IS A VECTOR WITH VALUE 1 IN ONLY ONE LOCATION AND ZEROES
C*    EVERYWHERE ELSE.  IN THESE CASES, THE FORWARD AND BACKWARD
C*    SUBSTITUTION METHODS CAN BE ABBREVIATED AND RESULT IN AN APPROX.
C*    3 FACTOR TIME SAVING.  A G VALUE EQUAL TO K INDICATES SUCH A
C*    CASE WITH THE 1 IN THE K-TH POSITION.  A G VALUE EQUAL TO 1
C*    INDICATES THAT A STANDARD MATRIX SOLUTION METHOD IS REQUIRED.
      ENTRY SOLUTN(A,F,G,N)
C
C***********************************************************************
C     FORWARD SOLUTION OF RIGHT HAND SIDE
C
      IENTRY=INT(G)
      NP1=N+1
      DO 220 I=IENTRY,N
      IM1=I-1
      FI=F(I)
      LI=((N+N-IENTRY)*(IENTRY-1))/2+I
      IF(I.EQ.IENTRY) GO TO 220
      DO 210 L=IENTRY,IM1
      FI=FI-A(LI)*F(L)
  210 LI=LI+N-L
220   F(I)=FI/A(LI)
C
C***********************************************************************
C*    BACKWARD SUBSTITUTION FOR FINAL SOLUTION
C
230   NN=(N*(N+1))/2
      F(N)=F(N)/A(NN)
      IF(IENTRY.EQ.N) RETURN
      II=NN
      IENTB=N+1-IENTRY
      DO 260 IB=2,IENTB
      I=N+1-IB
      II=II+I-NP1
      IP1=I+1
      FI=F(I)
      III=II-I
      DO 250 L=IP1,N
      IL=III+L
250   FI=FI-A(IL)*F(L)
260   F(I)=FI/A(II)
      RETURN
      END
	subroutine astr(d1,h,pp,s,p,np,dh,dpp,ds,dp,dnp)
c	this subroutine calculates the following five ephermides
c	of the sun and moon
c	h = mean longitude of the sum
c	pp = mean longitude of the solar perigee
c	s = mean longitude of the moon
c	p = mean longitude of the lunar perigee
c	np = negative of the longitude of the mean ascending node
c	and their rates of change.
c	Units for the ephermides are cycles and for their derivatives
c	are cycles/365 days
c	The formulae for calculating this ephermides were taken from
c	pages 98 and 107 of the Explanatory Supplement to the
c	Astronomical Ephermeris and the American Ephermis and
c	Nautical Almanac (1961)
c
	implicit real*8(a-h,o-z)
	real*8 np
	d2=d1*1.d-4
	f=360.d0
	f2=f/365.d0
	h=279.696678d0+.9856473354d0*d1+.00002267d0*d2*d2
	pp=281.220833d0+.0000470684d0*d1+.0000339d0*d2*d2+
     1  .00000007d0*d2**3
	s=270.434164d0+13.1763965268d0*d1-.000085d0*d2*d2+
     1  .000000039d0*d2**3
	p=334.329556d0+.1114040803d0*d1-.0007739d0*d2*d2-
     1  .00000026d0*d2**3
	np=-259.183275d0+.0529539222d0*d1-.0001557d0*d2*d2-
     1  .00000005d0*d2**3
	h=h/f
	pp=pp/f
	s=s/f
	p=p/f
	np=np/f
	h=h-dint(h)
	pp=pp-dint(pp)
	s=s-dint(s)
	p=p-dint(p)
	np=np-dint(np)
	dh=.9856473354d0+2.d-8*.00002267d0*d1
	dpp=.0000470684d0+2.d-8*.0000339d0*d1
     1  +3.d-12*.00000007d0*d1**2
	ds=13.1763965268d0-2.d-8*.000085d0*d1+
     1  3.d-12*.000000039d0*d1**2
	dp=.1114040803d0-2.d-8*.0007739d0*d1-
     1  3.d-12*.00000026d0*d1**2
	dnp=+.0529539222d0-2.d-8*.0001557d0*d1-
     1  3.d-12*.00000005d0*d1**2
	dh=dh/f2
	dpp=dpp/f2
	ds=ds/f2
	dp=dp/f2
	dnp=dnp/f2
	return
	end
      SUBROUTINE GDAY(IDD,IMM,IYY,ICC,KD)
C!
C!  GIVEN DAY,MONTH,YEAR AND CENTURY(EACH 2 DIGITS), GDAY RETURNS
C!  THE DAY#, KD BASED ON THE GREGORIAN CALENDAR.
C!  THE GREGORIAN CALENDAR, CURRENTLY 'UNIVERSALLY' IN USE WAS
C!  INITIATED IN EUROPE IN THE SIXTEENTH CENTURY. NOTE THAT GDAY
C!  IS VALID ONLY FOR GREGORIAN CALENDAR DATES.
C
C   KD=1 CORRESPONDS TO JANUARY 1, 0000
C	
c 	Note that the Gregorian reform of the Julian calendar 
c	omitted 10 days in 1582 in order to restore the date
c	of the vernal equinox to March 21 (the day after
c	Oct 4, 1582 became Oct 15, 1582), and revised the leap 
c	year rule so that centurial years not divisible by 400
c	were not leap years.
c
C   THIS ROUTINE WAS WRITTEN BY EUGENE NEUFELD, AT IOS, IN JUNE 1990.
C
      INTEGER NDP(13)
      INTEGER NDM(12)
      DATA NDP/0,31,59,90,120,151,181,212,243,273,304,334,365/
      DATA NDM/31,28,31,30,31,30,31,31,30,31,30,31/
C!
      LP = 6
C!  TEST FOR INVALID INPUT:
      IF(ICC.LT.0)THEN
	 WRITE(LP,5000)ICC
	 STOP
      ENDIF
      IF(IYY.LT.0.OR.IYY.GT.99)THEN
	 WRITE(LP,5010)IYY
	 STOP
      ENDIF
      IF(IMM.LE.0.OR.IMM.GT.12)THEN
	 WRITE(LP,5020)IMM
	 STOP
      ENDIF
      IF(IDD.LE.0)THEN
	 WRITE(LP,5030)IDD
	 STOP
      ENDIF
      IF(IMM.NE.2.AND.IDD.GT.NDM(IMM))THEN
	 WRITE(LP,5030)IDD
	 STOP
      ENDIF
      IF(IMM.EQ.2.AND.IDD.GT.29)THEN
	 WRITE(LP,5030)IDD
	 STOP
      ENDIF
      IF(IMM.EQ.2.AND.IDD.GT.28.AND.((IYY/4)*4-IYY.NE.0.OR.(IYY.EQ.0.AND
     .    .(ICC/4)*4-ICC.NE.0)))THEN
	 WRITE(LP,5030)IDD
	 STOP
      ENDIF
5000  FORMAT(' INPUT ERROR. ICC = ',I7)
5010  FORMAT(' INPUT ERROR. IYY = ',I7)
5020  FORMAT(' INPUT ERROR. IMM = ',I7)
5030  FORMAT(' INPUT ERROR. IDD = ',I7)
C!
C!  CALCULATE DAY# OF LAST DAY OF LAST CENTURY:
      KD = ICC*36524 + (ICC+3)/4
C!
C!  CALCULATE DAY# OF LAST DAY OF LAST YEAR:
      KD = KD + IYY*365 + (IYY+3)/4
C!
C!  ADJUST FOR CENTURY RULE:
C!  (VIZ. NO LEAP-YEARS ON CENTURYS EXCEPT WHEN THE 2-DIGIT
C!  CENTURY IS DIVISIBLE BY 4.)
      IF(IYY.GT.0.AND.(ICC-(ICC/4)*4).NE.0) KD=KD-1
C!  KD NOW TRULY REPRESENTS THE DAY# OF THE LAST DAY OF LAST YEAR.
C!
C!  CALCULATE DAY# OF LAST DAY OF LAST MONTH:
      KD = KD + NDP(IMM)
C!
C!  ADJUST FOR LEAP YEARS:
      IF(IMM.GT.2.AND.((IYY/4)*4-IYY).EQ.0.AND.((IYY.NE.0).OR.
     .   (((ICC/4)*4-ICC).EQ.0)))   KD=KD+1
C!  KD NOW TRULY REPRESENTS THE DAY# OF THE LAST DAY OF THE LAST
C!  MONTH.
C!
C!  CALCULATE THE CURRENT DAY#:
      KD = KD + IDD
      RETURN
C!
C!
      ENTRY DMY(IDD,IMM,IYY,ICC,KD)
C!
C!  GIVEN THE (GREGORIAN) DAY#, KD, AS CALCULATED ABOVE IN THIS ROUTINE,
C!  ENTRY DMY RETURNS THE (GREGORIAN) DAY, MONTH, YEAR AND CENTURY.
C!
C!  TEST FOR VALID INPUT:
      IF(KD.LE.0) WRITE(LP,5040)KD
5040  FORMAT(' KD = ',I7,'  INVALID INPUT. DMY STOP.')
C!
C!  SAVE KD
      KKD=KD
C!  CALCULATE ICC AND SUBTRACT THE NUMBER OF DAYS REPRESENTED BY ICC
C!  FROM KKD
C!  JFH IS THE NUMBER OF 400 YEAR INTERVALS UP TO KKD
C!  JCC IS THE NUMBER OF ADDITIONAL CENTURIES UP TO KKD
      JFH = KKD/146097
      KKD = KKD - JFH*146097
      IF(KKD.LT.36525)THEN
	 JCC = 0
      ELSE
	 KKD = KKD - 36525
	 JCC = 1 + KKD/36524
	 KKD = KKD - (JCC-1)*36524
      END IF
      ICC = 4*JFH + JCC
      IF(KKD.EQ.0)THEN
	 ICC = ICC-1
	 IYY = 99
	 IMM = 12
	 IDD = 31
	 RETURN
      ENDIF
C!
C!  CALCULATE IYY. JFY IS THE NUMBER OF FOUR YEAR INTERVALS IN THE
C!  CURRENT CENTURY. THE FIRST FOUR YEAR INTERVAL IS SHORT (1460 DAYS
C!  RATHER THAN 1461)IF THE CURRENT CENTURY IS NOT DIVISIBLE BY 4, AND
C!  IN THIS CASE JCC.NE.0 AS CALCULATED ABOVE.
C!
C!  CALCULATE JFY:
      JFY = 0
      IF(JCC.EQ.0)GOTO 10
      IF(KKD.LT.1460)GOTO 10
      JFY = 1
      KKD = KKD - 1460
10    KK = KKD/1461
      JFY = JFY + KK
      KKD = KKD - KK*1461
C!
C!  CALCULATE JYY, THE REMAINING YEARS OF THE CURRENT CENTURY UP TO THE
C!  CURRENT DAY:
      JYY = 0
C!  THE NEXT YEAR IS NOT A LEAP YEAR IF JFY=0 AND JCC.NE.0.
      IF(JFY.EQ.0.AND.JCC.NE.0)GOTO 20
      IF(KKD.LT.366)GOTO 30
      JYY = 1
      KKD = KKD - 366
20    JYYY = KKD/365
      JYY = JYY + JYYY
      KKD = KKD - JYYY*365
30    IYY = 4*JFY + JYY
      IF(KKD.EQ.0) THEN
	 IYY=IYY-1
	 IMM=12
	 IDD=31
	 RETURN
      END IF
C!
C!  SET L=1 IF WE HAVE A LEAP YEAR.
      L=0
      IF(IYY-(IYY/4)*4.NE.0)GOTO 40
      IF(IYY.EQ.0.AND.(ICC-(ICC/4)*4).NE.0)GOTO 40
      L=1
C!
C!  CALCULATE IMM AND IDD
40    IF(KKD.GT.31) GOTO 50
      IMM=1
      IDD=KKD
      RETURN
C!
50    IF(KKD.GT.59)GOTO 60
      IMM = 2
      IDD = KKD-31
      RETURN
C!
60    IF(KKD.GT.60)GOTO 70
      IF(L.EQ.0)GOTO 70
      IMM = 2
      IDD = 29
      RETURN
C!
70    IF(L.EQ.1) KKD=KKD-1
      DO 80 I=4,13
	 IF(KKD.GT.NDP(I))GOTO 80
	 IMM = I-1
	 IDD = KKD - NDP(I-1)
	 RETURN
C!
80    CONTINUE
90    WRITE(LP,5050)
5050  FORMAT(' ERROR IN DMY.')
      STOP
      END
